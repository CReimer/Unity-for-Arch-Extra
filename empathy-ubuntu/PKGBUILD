# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=empathy-ubuntu
_ubuntu_rel=0ubuntu1
_ubuntu_ver=3.6.0.3
_actual_ver=3.6.1
pkgver=${_actual_ver}.ubuntu${_ubuntu_ver}.${_ubuntu_rel}
pkgrel=100
pkgdesc="A GNOME instant messaging client using the Telepathy framework."
arch=('i686' 'x86_64')
url="http://live.gnome.org/Empathy"
license=('GPL2')
provides=("empathy=${_actual_ver}")
conflicts=('empathy')
depends=('clutter-gst' 'clutter-gtk' 'folks' 'gcr' 'iso-codes' 'libcanberra' 'libpulse' 'webkitgtk3' 'telepathy-farstream' 'telepathy-glib' 'telepathy-logger' 'telepathy-mission-control' 'signon' 'libsignon-glib' 'credentials-preferences')
makedepends=('intltool' 'itstool' 'docbook-xsl' 'nautilus-sendto')
optdepends=('telepathy-gabble: XMPP/Jabber support'
            'telepathy-idle: IRC support'
            'telepathy-salut: Link-local XMPP support'
            'telepathy-rakia: SIP support'
            'telepathy-haze: libpurple support'
            'nautilus-sendto: Send files from nautilus')
options=('!libtool')
groups=('gnome-extra')
install=empathy.install
source=("http://ftp.gnome.org/pub/GNOME/sources/${pkgname%-*}/${_actual_ver:0:3}/${pkgname%-*}-${_actual_ver}.tar.xz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/empathy_${_ubuntu_ver}-${_ubuntu_rel}.debian.tar.bz2")
sha512sums=('f0708617f2e903704ae8cad858317c3aca1c0dae4ccbd1be24faea4e32f7f7c8f75e3557ba208112936f65faa6a7a31a089b500f750cba85aa5a4414c804e43f'
            '23421fa0ed39990269b74d5704fa41b2042c2868685a1413a12d70108abef21300bd44ac769ae6e6ff8b9f935b85c914647fc780ea47a07b7272352650570e06')

build() {
  cd "${pkgname%-*}-${_actual_ver}"

  # Apply Ubuntu's patches
  for i in $(grep -v '#' "${srcdir}/debian/patches/series"); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  autoreconf -vfi

  PYTHON=python2 ./configure \
      --prefix=/usr \
      --sysconfdir=/etc  \
      --libexecdir=/usr/lib/empathy \
      --disable-static \
      --disable-schemas-compile \
      --enable-gst-1.0=yes \
      --enable-ubuntu-online-accounts=yes

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make -j1
}

package() {
  cd "${pkgname%-*}-${_actual_ver}"
  make -j1 DESTDIR="${pkgdir}" install

  install -dm755 "${pkgdir}/usr/share/indicators/messages/applications/"
  install -m644 "${srcdir}/debian/indicators/empathy" \
    "${pkgdir}/usr/share/indicators/messages/applications/"
}

# vim:set ts=2 sw=2 et:
