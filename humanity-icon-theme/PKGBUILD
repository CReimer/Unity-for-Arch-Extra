# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=humanity-icon-theme
pkgver=0.6.2
pkgrel=100
pkgdesc="Humanity Icon Theme"
arch=('any')
url="https://launchpad.net/humanity"
license=('GPL')
groups=('unity-extra')
depends=('gnome-icon-theme' 'hicolor-icon-theme')
makedepends=('icon-naming-utils')
provides=("humanity-icons=${pkgver}")
conflicts=('humanity-icons')
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${pkgver}.tar.gz")
sha512sums=('b9276505d056bba39732da0e22ed9a3d74cf965428a1c28c674992409f449cd41c6a4c801a3281c27da161548d3aae3c5926ac6762404eec27a6c7eefd365927')

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  install -dm755 "${pkgdir}"/usr/share/icons/

  # Install Humanity icon theme
  cp -a Humanity/ "${pkgdir}"/usr/share/icons/

  # Install Humanity-Dark icon theme
  cp -a Humanity-Dark/ "${pkgdir}"/usr/share/icons/

  # From debian/rules
  for i in $(find "${pkgdir}/usr/share/icons/" -mindepth 2 -maxdepth 2 -type d); do
    (
    cd "${i}"
    for j in *; do
      /usr/lib/icon-naming-utils/icon-name-mapping -c "${j}"
    done
    )
  done
}

# vim:set ts=2 sw=2 et:
