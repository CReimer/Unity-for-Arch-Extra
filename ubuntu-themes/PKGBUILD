# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=ubuntu-themes
_actual_ver=13.04
_extra_ver=daily12.12.11
pkgver=${_actual_ver}${_extra_ver}
pkgrel=100
pkgdesc="Ubuntu monochrome icon theme, Abmiance and Radiance themes, and Ubuntu artwork"
arch=('any')
url="https://launchpad.net/ubuntu-themes"
license=('CCPL')
groups=('unity-extra')
depends=('humanity-icon-theme' 'gnome-icon-theme' 'hicolor-icon-theme' 'gtk-engine-unico' 'gtk-engine-murrine')
makedepends=('python2' 'imagemagick' 'icon-naming-utils')
provides=('light-themes' 'ubuntu-mono')
conflicts=('light-themes' 'ubuntu-mono')
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${_actual_ver}${_extra_ver}.orig.tar.gz")
sha512sums=('8788b9d80f9ad2eb67025f5272a390e66f2b9fb3c3b9d2e9241a1922adc210f1cee8f96bcbf1d5a26c83f13216ebb90c2a1469b84d1197c114f2243c32693719')

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  # Python 2 fix
  sed -i 's|^\(#!.*python$\)|\12|g' scavenge.py

  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  # Install themes
  install -dm755   "${pkgdir}/usr/share/themes/"
  cp -av Ambiance/ "${pkgdir}/usr/share/themes/"
  cp -av Radiance/ "${pkgdir}/usr/share/themes/"

  # Install icons
  install -dm755            "${pkgdir}/usr/share/icons/"
  cp -av ubuntu-mono-dark/  "${pkgdir}/usr/share/icons/"
  cp -av ubuntu-mono-light/ "${pkgdir}/usr/share/icons/"
  cp -av LoginIcons/        "${pkgdir}/usr/share/icons/"

  # From debian/rules: What's the purpose of this?
  for i in "${pkgdir}"/usr/share/icons/ubuntu-mono-*; do
    cp -avt ${i} places/
  done

  install -Dm644 distributor-logo.png \
    "${pkgdir}/usr/share/icons/hicolor/48x48/apps/distributor-logo.png"
}

# vim:set ts=2 sw=2 et:
