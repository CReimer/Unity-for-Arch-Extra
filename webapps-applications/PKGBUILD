# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=webapps-applications
_actual_ver=2.4.16
_extra_ver=+13.10.20130907
pkgver=${_actual_ver}daily13.09.07
pkgrel=100
pkgdesc="Unity WebApps integration scripts"
arch=('any')
url="https://launchpad.net/webapps-applications"
license=('GPL')
groups=('unity-extra')
depends=('glib2' 'gtk3' 'libunity-webapps' 'unity-asset-pool' 'webkitgtk3' 'xdg-utils')
makedepends=('intltool')
optdepends=('gjs: Development (or something)'
            'python-polib: I have no idea')
install=webapps-applications.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/webapps-applications_${_actual_ver}${_extra_ver}.orig.tar.gz")
sha512sums=('c201d4a3ea7ad1fea23618a2fbf2c3853287cb9fcd8b18ae6140133c17931f2e8bc2f9f29989d335f0f63343fd0d287f84c6364e49c765d796e57600050509d8')

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  autoreconf -vfi

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  make DESTDIR="${pkgdir}/" install

  install -dm755 "${pkgdir}/usr/share/unity-webapps/tools/"
  install -m644 scripts/po2json.py \
                scripts/applinter.sh \
                scripts/style_checker.js \
                scripts/third_party/jslint.js \
                "${pkgdir}/usr/share/unity-webapps/tools/"
  install -m644 common/rules.mk "${pkgdir}/usr/share/unity-webapps/"

  rm "${pkgdir}/usr/share/unity-webapps/userscripts/unity-webapps-amazon/Amazon.user.js"
  rm "${pkgdir}/usr/share/unity-webapps/userscripts/unity-webapps-amazon/manifest.json"
}

# vim:set ts=2 sw=2 et:
