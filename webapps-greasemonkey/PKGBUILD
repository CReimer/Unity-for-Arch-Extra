# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

OLDDATE=$(date +%Y.%m.%d)

# Upstream package name is webapps-greasemonkey
# Ubuntu's package name is xul-ext-websites-integration

pkgname=firefox-extension-websites-integration
pkgver=2.3.5.2
pkgrel=101
pkgdesc="Firefox extension to support user scripts"
arch=('any')
url="https://launchpad.net/webapps-greasemonkey"
license=('GPL')
groups=('unity-extra')
depends=('libunity-webapps')
makedepends=('zip' 'unzip')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/webapps-greasemonkey_${pkgver}.orig.tar.gz"
        '0001_Fix_library_loading.patch')
sha512sums=('4d1b3ef16649f11cd6ecb802234fdc5c1cbb1858dd25be225c6957aeb96d1a8bddf86a8d28f1f8303c6630e9cf4979821e7d30d3c9808a324f6384ff356207e8'
            '531f36c051f2690bb40c68cc89d73213f3359b1e15f50b6e442888e6304d06867607d61e1dcccefc6d27428154f7aeb02fb586260d91e32bc3f8c093e88b7f3f')

prepare() {
  cd "${srcdir}/webapps-greasemonkey-${pkgver}"
  patch -p1 -i "${srcdir}/0001_Fix_library_loading.patch"
}

build() {
  cd "${srcdir}/webapps-greasemonkey-${pkgver}"
  ./build.sh
}

package() {
  cd "${srcdir}/webapps-greasemonkey-${pkgver}"

  local emid=$(sed -n 's/.*<em:id>\(.*\)<\/em:id>.*/\1/p' install.rdf | head -1)
  install -dm755 "${pkgdir}/usr/lib/firefox/extensions/${emid}/"
  NEWDATE=$(date +%Y.%m.%d)
  if [[ "${OLDDATE}" != "${NEWDATE}" ]]; then
    error "Do not build at midnight :) Please rebuild."
    exit 1
  fi
  unzip webapps-${NEWDATE}.beta.xpi -d \
    "${pkgdir}/usr/lib/firefox/extensions/${emid}/"
}

# vim:set ts=2 sw=2 et:
