# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Taken from M0Rf30 morf3089 at gmail dot com

pkgname=lightdm-unity-greeter
_ubuntu_rel=0ubuntu5
pkgver=12.10.2.${_ubuntu_rel}
pkgrel=100
pkgdesc="The greeter (login screen) application for Unity. It is implemented as a LightDM greeter."
arch=('i686' 'x86_64')
url="https://launchpad.net/unity-greeter"
license=('GPL3' 'LGPL3')
groups=('unity-extra')
depends=('gnome-doc-utils' 'lightdm-ubuntu' 'gnome-common' 'libindicator3' 'cantarell-fonts' 'light-themes' 'ubuntu-wallpapers' 'ttf-ubuntu-font-family')
makedepends=('gnome-settings-daemon' 'pkg-config' 'vala>=0.16.0' 'autoconf' 'archlinux-artwork' 'imagemagick')
options=('!libtool')
install=unity-greeter.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname#*-}_${pkgver%.*}-${_ubuntu_rel}.debian.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname#*-}_${pkgver%.*}.orig.tar.gz"
        "valaflags.patch")
sha512sums=('35755a418ed2f25c041ab539c9ecc8f628632c9051c0241bf52338df9ca3144fa532286ca3933b866ae77f3f5e62d6af6dcc5a074010b66a2b412c44617f7312'
            '1bb458200c9090e17c39f3d6978e64ea74f4157c0c7fa1ed074aff15d98e1e2b734f929b117de9460b4989f615e84c0023102f455b106220044259e31730bb5a'
            'e231780d657abbd2c9990acc3eb03ec5628c8bcfd0ef42c459a1e30a9227e0377c687a977e1d510ec2c0d375963469752fdd9c7bff686c8fd9b8feeabd55a7ca')

build() {
  cd "${srcdir}/${pkgname#*-}-${pkgver%.*}"

  # Apply Ubuntu patches
  for i in $(cat "${srcdir}/debian/patches/series" | grep -v '#'); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  # Patch by ecopunk to fix FTBFS on some systems
  #patch -Np1 -i "${srcdir}/valaflags.patch"

  # Use Arch Linux logo (retain the same 245px height as the Ubuntu logo)
  rm data/logo.png
  convert -background none -resize 245 \
    /usr/share/archlinux/logos/archlinux-mono-white.svg \
    data/logo.png

  # Link against libm and libx11
  CFLAGS="${CFLAGS} -lm -lX11"

  autoreconf -vfi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/lightdm

   make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/unity-greeter-${pkgver%.*}"

  make DESTDIR="${pkgdir}" install

  # Install PolicyKit file for allowing the lightdm user to use NetworkManager
  install -dm700 "${pkgdir}/var/lib/polkit-1/"
  install -dm755 "${pkgdir}/var/lib/polkit-1/localauthority/10-vendor.d/"
  install -m644 "${srcdir}/debian/unity-greeter.pkla" \
    "${pkgdir}/var/lib/polkit-1/localauthority/10-vendor.d/"

  # Install Remote Login help icon
  install -m644 "${srcdir}/debian/remote_login_help.png" \
    "${pkgdir}/usr/share/unity-greeter/"
}

# vim:set ts=2 sw=2 et:
