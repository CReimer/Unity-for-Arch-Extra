# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Taken from M0Rf30 morf3089 at gmail dot com

pkgname=lightdm-unity-greeter
_ubuntu_rel=0ubuntu3
pkgver=12.10.4.${_ubuntu_rel}
pkgrel=100
pkgdesc="The greeter (login screen) application for Unity. It is implemented as a LightDM greeter."
arch=('i686' 'x86_64')
url="https://launchpad.net/unity-greeter"
license=('GPL3' 'LGPL3')
groups=('unity-extra')
depends=('gnome-doc-utils' 'lightdm-ubuntu' 'gnome-common' 'libindicator3' 'cantarell-fonts' 'light-themes' 'ubuntu-wallpapers' 'ttf-ubuntu-font-family')
makedepends=('gnome-settings-daemon' 'pkg-config' 'vala>=0.16.0' 'autoconf' 'archlinux-artwork' 'imagemagick')
options=('!libtool')
install=unity-greeter.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname#*-}_${pkgver%.*}-${_ubuntu_rel}.debian.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname#*-}_${pkgver%.*}.orig.tar.gz"
        "valaflags.patch")
sha512sums=('53331bfea19f2bb3746f563fa50eb0dcf78803e062034dbdf163f89b8402efdce411f97fd713c5f0f1d93235e227c80fe10f6c60247cd10e28ab0b85b62e6389'
            'd3518a7bf12be4ae8b08cd4c63aaae224c5b20aaf92f187e9a502ca1173137bf4c5c57b921b579123d95686bb283e53ff90fb2a82863eb57470ad2bb1103b3b4'
            'e231780d657abbd2c9990acc3eb03ec5628c8bcfd0ef42c459a1e30a9227e0377c687a977e1d510ec2c0d375963469752fdd9c7bff686c8fd9b8feeabd55a7ca')

build() {
  cd "${srcdir}/${pkgname#*-}-${pkgver%.*}"

  # Apply Ubuntu patches
  for i in $(cat "${srcdir}/debian/patches/series" | grep -v '#'); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  # Patch by ecopunk to fix FTBFS on some systems
  #patch -Np1 -i "${srcdir}/valaflags.patch"

  # Use Arch Linux logo (retain the same 245px height as the Ubuntu logo)
  rm data/logo.png
  convert -background none -resize 245 \
    /usr/share/archlinux/logos/archlinux-mono-white.svg \
    data/logo.png

  # Link against libm and libx11
  CFLAGS="${CFLAGS} -lm -lX11"

  autoreconf -vfi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/lightdm

   make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/unity-greeter-${pkgver%.*}"

  make DESTDIR="${pkgdir}" install

  # Install PolicyKit file for allowing the lightdm user to use NetworkManager
  install -dm700 "${pkgdir}/var/lib/polkit-1/"
  install -dm755 "${pkgdir}/var/lib/polkit-1/localauthority/10-vendor.d/"
  install -m644 "${srcdir}/debian/unity-greeter.pkla" \
    "${pkgdir}/var/lib/polkit-1/localauthority/10-vendor.d/"
}

# vim:set ts=2 sw=2 et:
