# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgbase=webaccounts-browser-extension
pkgname=('webaccounts-browser-extension' 'chromium-extension-webaccounts' 'firefox-extension-webaccounts')
_ubuntu_rel=0ubuntu1
pkgver=0.5
pkgrel=100
pkgdesc="Ubuntu Online Accounts extensions for Firefox and Chromium"
arch=('i686' 'x86_64')
url="https://launchpad.net/webaccounts-browser-extension"
license=('GPL' 'LGPL')
groups=('unity-extra')
depends=('json-glib' 'libaccounts-glib' 'openssl')
makedepends=('gnome-common' 'intltool' 'unzip' 'vim' 'zip')
checkdepends=('dbus-test-runner' 'gtest-ubuntu')
extrafiles=('webaccounts-browser-extension.install')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/webaccounts-browser-extension_${pkgver}.orig.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/webaccounts-browser-extension_${pkgver}-${_ubuntu_rel}.diff.gz")
sha512sums=('0f6fba58defa0f67faf504704f202f387066dde813e2446a2ee179a310801f236806b2398bf205cc17942375d98298a3d664ec50ff72b470954f5160f1ecb11e'
            'fd4b02998fede99f2bedbf50f5c771c8545bef6f961b725b82a64e5a0332a07e0cb959863bf56164ca4164c518561126283603f8ee7ac4d2c168abffe929df5f')

prepare() {
  cd "${srcdir}/webaccounts-browser-extension-${pkgver}"

  patch -p1 -i "${srcdir}/webaccounts-browser-extension_${pkgver}-${_ubuntu_rel}.diff"
}

build() {
  cd "${srcdir}/webaccounts-browser-extension-${pkgver}"

  cp debian/webaccounts.pem .

  NOCONFIGURE=1 ./autogen.sh

  ./configure --prefix=/usr
  make
}

check() {
  cd "${srcdir}/webaccounts-browser-extension-${pkgver}"

  dbus-test-runner -t make -p check -m 180
}

package_webaccounts-browser-extension() {
  pkgdesc="Common files for the Firefox and Chromium webaccounts extensions"
  options=('!libtool')
  install=webaccounts-browser-extension.install

  cd "${srcdir}/webaccounts-browser-extension-${pkgver}"
  make DESTDIR="${pkgdir}/" install

  rm -rv "${pkgdir}/usr/lib/webaccounts-chromium/"
  rm -rv "${pkgdir}/usr/lib/webaccounts-firefox/"
  rm -rv "${pkgdir}/usr/share/chromium/"
}

package_chromium-extension-webaccounts() {
  pkgdesc="Ubuntu Online Accounts extension for Chromium"
  depends=("webaccounts-browser-extension=${pkgver}" 'chromium' 'credentials-preferences')

  cd "${srcdir}/webaccounts-browser-extension-${pkgver}"

  make -C chromium-extension DESTDIR="${pkgdir}/" install
}

package_firefox-extension-webaccounts() {
  pkgdesc="Ubuntu Online Accounts extension for Firefox"
  depends=("webaccounts-browser-extension=${pkgver}" 'firefox' 'credentials-preferences')

  cd "${srcdir}/webaccounts-browser-extension-${pkgver}"

  local emid=$(sed -n 's/.*<em:id>\(.*\)<\/em:id>.*/\1/p' \
    firefox-extension/install.rdf | head -1)
  install -dm755 "${pkgdir}/usr/lib/firefox/extensions/${emid}/"
  unzip firefox-extension/webaccounts-firefox-extension.xpi -d \
    "${pkgdir}/usr/lib/firefox/extensions/${emid}/"
}

# vim:set ts=2 sw=2 et:
