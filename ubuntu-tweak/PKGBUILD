# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=ubuntu-tweak
pkgver=0.7.2
pkgrel=100
pkgdesc="An easy configuration tool for system and desktop settings"
arch=('any')
url="http://ubuntu-tweak.com/"
license=('GPL')
groups=('unity-extra')
depends=('compizconfig-python-ubuntu' 'python2' 'gconf' 'libunique3' 'hicolor-icon-theme' 'python2-defer' 'python2-lxml')
# Also depends on polkit-gnome or polkit-qt
makedepends=('python2-distutils-extra' 'python2-distribute')
options=(!emptydirs)
install=${pkgname}.install
source=("https://launchpad.net/${pkgname}/${pkgver%.*}.x/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz"
        '01_arch_linux.patch')
sha512sums=('91931364569166fd422d4be714ac574efc6ca7e047958dc41397239a32c96fcc67483933883c21f8475c117137fcd05288d5af919cc0d8edf248caa59bbce230'
            'baea2a9bec9de580f1697b0af947453a2b4f3f2131bd2e2f1235afe3b97f7480db295b818007207e89da210f65b88731041a0f75beb4932b9fe2ff4c2ee36375')

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # A patch created by me that
  #  - Removes any apt/dpkg/deb related functions
  #  - Removes any checks and references to other distros and replaces them
  #    with something appropriate for Arch Linux
  #  - Remove any features that require root (only Login manager right now)
  #    since they use a PolicyKit API exposed by aptdaemon.
  #  - Remove code that handles ut:// URL's
  patch -Np1 -i "${srcdir}/01_arch_linux.patch"

  python2 setup.py install --root="${pkgdir}/" --optimize=1

  # Python 2 fix
  sed -i 's|\(#!.*bin.*python$\)|\12|g' $(grep -rl '#!.*python[ ]*$' "${pkgdir}")
}

# vim:set ts=2 sw=2 et:
