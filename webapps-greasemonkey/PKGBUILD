# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

OLDDATE=$(date +%Y.%m.%d)

# Upstream package name is webapps-greasemonkey
# Ubuntu's package name is xul-ext-websites-integration

pkgname=firefox-extension-websites-integration
pkgver=2.3.4
pkgrel=100
pkgdesc="Firefox extension to support user scripts"
arch=('any')
url="https://launchpad.net/webapps-greasemonkey"
license=('GPL')
groups=('unity-extra')
depends=('libunity-webapps')
makedepends=('zip')
source=("https://launchpad.net/webapps-greasemonkey/trunk/${pkgver}/+download/webapps-greasemonkey-${pkgver}.tar.gz")
sha512sums=('7ddabb451ce6fff02141684f79ec290eb181df0872ef6e093472043c2fffaaf450ee221d20c8b98f350e550587581a49f1ecc91902b3720f67412b33c3acbb86')

build() {
  cd "${srcdir}/webapps-greasemonkey-${pkgver}"
  ./build.sh
}

package() {
  cd "${srcdir}/webapps-greasemonkey-${pkgver}"

  local emid=$(sed -n 's/.*<em:id>\(.*\)<\/em:id>.*/\1/p' install.rdf | head -1)
  install -dm755 "${pkgdir}/usr/lib/firefox/extensions/${emid}/"
  NEWDATE=$(date +%Y.%m.%d)
  if [[ "${OLDDATE}" != "${NEWDATE}" ]]; then
    error "Do not build at midnight :) Please rebuild."
    exit 1
  fi
  unzip webapps-${NEWDATE}.beta.xpi -d \
    "${pkgdir}/usr/lib/firefox/extensions/${emid}/"
}

# vim:set ts=2 sw=2 et:
