# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=gtk-engine-unico
_ubuntu_rel=0ubuntu5
pkgver=1.0.1.${_ubuntu_rel}
pkgrel=100
pkgdesc="Complete, yet powerful theming engine for GTK+ 3.0 and newer"
arch=('i686' 'x86_64')
url="https://launchpad.net/unico"
license=('LGPL')
groups=('unity-extra')
depends=('gtk3')
source=("https://launchpad.net/unico/${pkgver%.*.*}/${pkgver%.*}/+download/unico-${pkgver%.*}.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/gtk3-engines-unico_${pkgver%.*}-${_ubuntu_rel}.diff.gz")
sha512sums=('8fb9ad8a80ad1f63c87c278761c8a7a1b4a89d698173537c022595c7fc70746f1bde1df3f4a810e3e5e6d0285b0dd05292d26dbd01fe841af86d6390bdea7756'
            '3c44e0b7d19ba15016af17a6f012cb932e0820a32b6b91bdec2f683817edb5a41b6485996f89808434823af5d4a340abf8365be3c124d96c75fdf527e8956ee6')

build() {
  cd "${srcdir}/unico-${pkgver%.*}"

  # Apply Ubuntu patches
  patch -Np1 -i "${srcdir}/gtk3-engines-unico_${pkgver%.*}-${_ubuntu_rel}.diff"

  autoreconf -vfi

  ./configure --prefix=/usr
  make ${MAKEFLAGS}
}

check() {
  cd "${srcdir}/unico-${pkgver%.*}"
  make -k check
}

package() {
  cd "${srcdir}/unico-${pkgver%.*}"
  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
