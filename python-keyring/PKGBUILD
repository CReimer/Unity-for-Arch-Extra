# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgbase=python-keyring
pkgname=('python-keyring' 'python2-keyring')
pkgver=1.2.2
pkgrel=103
pkgdesc="Python library for accessing keyring services"
arch=('any')
url="http://pypi.python.org/pypi/keyring"
license=('python')
groups=('unity-extra')
makedepends=('python' 'python2' 'python-distribute' 'python2-distribute' 'zip' 'unzip')
source=("http://pypi.python.org/packages/source/k/keyring/keyring-${pkgver}.zip")
noextract=("keyring-${pkgver}.zip")
sha512sums=('2fcc0cf0b78cbea47464cfbf35ed0be8558399350e05f7db0ed68188d26752d56e86fa7c0912b4e711ed4159927c57e649cba8d508938f88e33b8bef786854bb')

package_python-keyring() {
  depends=('python-crypto')

  unzip "${srcdir}/keyring-${pkgver}.zip" -d "${srcdir}/build-python3/"
  cd "${srcdir}/build-python3/keyring-${pkgver}/"
  LANG="en_US.UTF-8" python setup.py install --root="${pkgdir}/" --optimize=1
}

package_python2-keyring() {
  depends=('python2-crypto')

  unzip "${srcdir}/keyring-${pkgver}.zip" -d "${srcdir}/build-python2/"
  cd "${srcdir}/build-python2/keyring-${pkgver}/"
  python2 setup.py install --root="${pkgdir}/" --optimize=1
  rm "${pkgdir}/usr/bin/keyring"
}

# vim:set ts=2 sw=2 et:
