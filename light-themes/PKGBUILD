# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=light-themes
_ubuntu_rel=0ubuntu1
pkgver=0.1.9.1.${_ubuntu_rel}
pkgrel=100
pkgdesc="Light Themes (Ambiance and Radiance)"
arch=('any')
url="https://launchpad.net/light-themes"
license=('CCPL')
groups=('unity-extra')
depends=('ubuntu-mono' 'gtk-engine-unico' 'gtk-engine-murrine' 'humanity-icon-theme' 'gconf-ubuntu')
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${pkgver%.*}-${_ubuntu_rel}.tar.gz")
sha512sums=('a4e20dedf2a7b79a234f83c0cfd158a540a8159d9918bc6a11f87993e55d62f8bad5854cb0f3c85ec1ff614c90ccaeb0755f46300c878c5b8f7e5c6c067b952c')

package() {
  cd "${srcdir}/${pkgname}-${pkgver%.*}"

  find Ambiance -type f -exec install -Dm644 {} "${pkgdir}"/usr/share/themes/{} \;
  find Radiance -type f -exec install -Dm644 {} "${pkgdir}"/usr/share/themes/{} \;

  # GConf defaults
  install -Dm644 debian/gconf-defaults "${pkgdir}/usr/share/gconf/defaults/90_${pkgname}"
}

# vim:set ts=2 sw=2 et:
