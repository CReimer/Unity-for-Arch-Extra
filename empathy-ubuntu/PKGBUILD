# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=empathy-ubuntu
_ppa_rel=0ubuntu1~trusty1
_ppa_ver=3.11.91
pkgver=3.12.0
pkgrel=100
pkgdesc="A GNOME instant messaging client using the Telepathy framework."
arch=('i686' 'x86_64')
url="http://live.gnome.org/Empathy"
license=('GPL2')
provides=("empathy=${pkgver}")
conflicts=('empathy')
depends=('clutter-gst' 'clutter-gtk' 'folks' 'gcr' 'iso-codes' 'libcanberra' 'libpulse' 'webkitgtk' 'telepathy-farstream' 'telepathy-glib' 'telepathy-logger' 'telepathy-mission-control' 'geoclue2' 'geocode-glib' 'cheese' 'libchamplain' 'libunity' 'ido' 'unity-asset-pool')
makedepends=('gnome-common' 'intltool' 'itstool' 'docbook-xsl' 'yelp-tools' 'python2')
optdepends=('telepathy-gabble: XMPP/Jabber support'
            'telepathy-idle: IRC support'
            'telepathy-salut: Link-local XMPP support'
            'telepathy-rakia: SIP support'
            'telepathy-haze: libpurple support')
options=('!makeflags')
groups=('gnome')
install=empathy.install
source=("http://ftp.gnome.org/pub/GNOME/sources/${pkgname%-*}/${pkgver%.*}/${pkgname%-*}-${pkgver}.tar.xz"
        "http://ppa.launchpad.net/gnome3-team/gnome3-staging/ubuntu/pool/main/e/empathy/empathy_${_ppa_ver}-${_ppa_rel}.debian.tar.gz")
sha512sums=('28260b33fb00aed777e7610182fd8c0781aa2175606b830053460a968ebd3e845ee00e0cb704338438028606bf0465a5925bced1a7bd61005ed0c266996e30b1'
            '534aff866d5bf5fc9014ae9d73274aeef0150564aa4b5ca35891734d213437fec8ea7730857c9aa6450e3440dee9e6544ac7508c86acbd39c6d1f8002b24e25e')

prepare() {
  cd "${pkgname%-*}-${pkgver}"

  # Apply Ubuntu's patches
    # Disable patches
      # Do not use Ubuntu Online Accounts
        sed -i '/ubuntu_launch_uoa_panel.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/00_linker-fixes.patch/d' "${srcdir}/debian/patches/series"

  for i in $(grep -v '#' "${srcdir}/debian/patches/series"); do
    patch -p1 -i "${srcdir}/debian/patches/${i}"
  done
}

build() {
  cd "${pkgname%-*}-${pkgver}"

  #autoreconf -vfi

  PYTHON=python2 ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc  \
    --libexecdir=/usr/lib/empathy \
    --disable-static \
    --disable-schemas-compile \
    --enable-gst-1.0=yes \
    --enable-spell=yes \
    --enable-webkit=yes \
    --enable-map=yes \
    --enable-location=yes \
    --enable-geocode=yes \
    --enable-gudev=yes \
    --enable-call-logs=yes \
    --enable-call=yes \
    --enable-ubuntu-online-accounts=no \
    --enable-goa=yes \
    --enable-libunity=yes \
    --enable-control-center-embedding=yes \
    --with-connectivity=nm

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make -j1
}

package() {
  cd "${pkgname%-*}-${pkgver}"
  make -j1 DESTDIR="${pkgdir}" install

  install -dm755 "${pkgdir}/usr/share/indicators/messages/applications/"
  install -m644 "${srcdir}/debian/indicators/empathy" \
    "${pkgdir}/usr/share/indicators/messages/applications/"
}
