# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# Upstream package name is unity-firefox-extension

pkgname=firefox-extension-unity
pkgver=2.4.4bzr13.01.29
pkgrel=100
pkgdesc="Firefox extension for Unity integration"
arch=('i686' 'x86_64')
url="https://launchpad.net/unity-firefox-extension"
license=('GPL')
groups=('unity-extra')
depends=('firefox-extension-websites-integration' 'libunity-webapps')
makedepends=('gtk2' 'libxslt' 'zip' 'unzip')
options=('!libtool')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/unity-firefox-extension_${pkgver}.orig.tar.gz"
        'launcher_bfb.png')
sha512sums=('9fce553904452ae14fdd27e915a470810817512726eb9b4d502a581ba5b7fe73fafeb34cf7312c9dc64a06d6c6e452c9facbf856777e799448e4529f9e61be53'
            '9f5469c0300d0c33e26db670771803efd3d821e82689fbb1391a31b8fd0e280a12ea010fe6f32a702a5d1797918d416c8adabfccf6049e43b496ebb6dbe2c054')

build() {
  cd "${srcdir}/unity-firefox-extension-${pkgver}"

  pushd libufe-xidgetter/
  autoreconf -vfi
  ./configure --prefix=/usr --disable-static
  make ${MAKEFLAGS}
  popd

  pushd unity-firefox-extension/
  bash ./build.sh
  popd
}

package() {
  cd "${srcdir}/unity-firefox-extension-${pkgver}"

  pushd libufe-xidgetter/
  make DESTDIR="${pkgdir}/" install
  popd

  pushd unity-firefox-extension/
  local emid=$(sed -n 's/.*<em:id>\(.*\)<\/em:id>.*/\1/p' install.rdf | head -1)
  install -dm755 "${pkgdir}/usr/lib/firefox/extensions/${emid}/"
  unzip unity.xpi -d "${pkgdir}/usr/lib/firefox/extensions/${emid}/"
  popd

  # Arch Linux logo
  rm "${pkgdir}/usr/lib/firefox/extensions/${emid}/skin/cof.png"
  install -m644 "${srcdir}/launcher_bfb.png" \
    "${pkgdir}/usr/lib/firefox/extensions/${emid}/skin/cof.png"
}

# vim:set ts=2 sw=2 et:
