# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=accountsservice-ubuntu
_ubuntu_rel=2ubuntu9.1
pkgver=0.6.15.${_ubuntu_rel}
pkgrel=100
pkgdesc="D-Bus interface for user account query and manipulation"
arch=('i686' 'x86_64')
url="http://cgit.freedesktop.org/accountsservice/"
license=('GPL3')
depends=('glib2' 'polkit' 'libsystemd' 'lightdm-ubuntu')
makedepends=('intltool' 'gobject-introspection' 'vala')
provides=("accountsservice=${pkgver%.*.*}")
conflicts=('accountsservice')
options=('!libtool')
source=("http://cgit.freedesktop.org/accountsservice/snapshot/${pkgname%-*}-${pkgver%.*.*}.tar.xz"
        "http://archive.ubuntu.com/ubuntu/pool/main/a/accountsservice/accountsservice_${pkgver%.*.*}-${_ubuntu_rel}.debian.tar.gz")
sha512sums=('90e11e71cc2ecb250e23449c11a2c8ceee3a9797cf6cf241eb64a73c12d460a281380099d34b0f9e8cdf82ef25f9c584a73362c90f51680d0f4412d7572cb5ce'
            'cf04215d37a625020952449de639987058210b5b1120e13573026628a52975568c2be0349c78bf9dc8905a1b62c4829d53032fb7c77cf2a7d883e17a03f0a51e')

build() {
  cd "${srcdir}/${pkgname%-*}-${pkgver%.*.*}"

  # Apply Ubuntu patches
    # Disable patches
      # Arch Linux uses wheel group for system administration
        sed -i '/0002-create-and-manage-groups-like-on-a-debian-system.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
        sed -i '/0002-create-and-manage-groups-like-on-a-ubuntu-system.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
      # Arch Linux uses /etc/gdm as the GDM configuration directory
        sed -i '/0005-gdm_config_file_path.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
      # adduser is Debian-specific
        sed -i '/0006-adduser_instead_of_useradd.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
      # Language patches are still pretty specific to Ubuntu (Ubuntu is trying
      # to get them merged upstream)
        sed -i '/0001-formats-locale-property.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
        sed -i '/0009-language-tools.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
        sed -i '/0010-set-language.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
      # Merged Upstream
        #sed -i '/git-icon-reset.patch/d' \
        #  "${srcdir}"/debian/patches/{ubuntu.,}series
      # Patches not applied in Ubuntu
        sed -i '/0003-getpwnam_null_crash.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
        sed -i '/0004-history-filter-sessions.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
  for i in $(cat "${srcdir}/debian/patches/ubuntu.series" | grep -v '#'); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/accountsservice \
    --with-systemdsystemunitdir=/usr/lib/systemd/system \
    --localstatedir=/var \
    --disable-static

  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname%-*}-${pkgver%.*.*}"

  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
