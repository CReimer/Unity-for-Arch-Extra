# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=ubuntu-tweak
pkgver=0.7.1
pkgrel=100
pkgdesc="An easy configuration tool for system and desktop settings"
arch=('any')
url="http://ubuntu-tweak.com/"
license=('GPL')
groups=('unity-extra')
depends=('compizconfig-python-ubuntu' 'python2' 'gconf' 'libunique3' 'hicolor-icon-theme' 'python2-defer' 'python2-lxml')
# Also depends on polkit-gnome or polkit-qt
makedepends=('python-distutils-extra' 'python2-distribute')
options=(!emptydirs)
install=${pkgname}.install
source=("https://launchpad.net/${pkgname}/${pkgver%.*}.x/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz"
        '01_arch_linux.patch')
sha512sums=('1c226b776d257c66232c92d3bac6f1dc372b61ab1385957959fa408fdca71cc21171dd5b1798bd6222782001cc370331d0b35e4ae091a950b694d441f75180b2'
            '2cc52bc7a601436378e0f5ff7ce2fd4f7ce799e48732c9f46ffdc7ddd2dc90b46306111b4459cf94dbde3f564366211ff055ceafc85fc845c52d7a5a9d342534')

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # A patch created by me that
  #  - Removes any apt/dpkg/deb related functions
  #  - Removes any checks and references to other distros and replaces them
  #    with something appropriate for Arch Linux
  #  - Remove any features that require root (only Login manager right now)
  #    since they use a PolicyKit API exposed by aptdaemon.
  #  - Remove code that handles ut:// URL's
  patch -Np1 -i "${srcdir}/01_arch_linux.patch"

  python2 setup.py install --root="${pkgdir}/" --optimize=1

  # Python 2 fix
  sed -i 's|\(#!.*bin.*python$\)|\12|g' $(grep -rl '#!.*python[ ]*$' "${pkgdir}")
}

# vim:set ts=2 sw=2 et:
