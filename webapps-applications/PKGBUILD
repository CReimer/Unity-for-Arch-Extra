# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=webapps-applications
pkgver=2.4.14
pkgrel=101
pkgdesc="Unity WebApps integration scripts"
arch=('any')
url="https://launchpad.net/webapps-applications"
license=('GPL')
groups=('unity-extra')
depends=('libunity-webapps' 'xdg-utils')
makedepends=('python-polib' 'intltool')
install=webapps-applications.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/webapps-applications_${pkgver}.orig.tar.gz")
sha512sums=('fc04d141477cc8f58dcd6e9e0c4265fce892fdfb8b03eaf9fc885651fb8c5c4540d761014f338e8175c432773d1c6623bd8b174f8c04ebab327af7426b014656')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  autoreconf -vfi
  intltoolize -f

  ./configure --prefix=/usr --enable-default-applications
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install

  install -dm755 "${pkgdir}/usr/bin/"
  install -m755 scripts/install-default-webapps-in-launcher.py \
    "${pkgdir}/usr/bin/webapps-add-defaults-to-launcher"

  # What is heck is zn_CN? zh stands for the first two letter of the pinyin of
  # 中文 (zhongwen)
  mv "${pkgdir}"/usr/share/locale/{zn,zh}_CN/
}

# vim:set ts=2 sw=2 et:
