# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=webapps-applications
_actual_ver=2.4.16
_extra_ver=+13.10.20130719
pkgver=${_actual_ver}daily13.07.19
pkgrel=100
pkgdesc="Unity WebApps integration scripts"
arch=('any')
url="https://launchpad.net/webapps-applications"
license=('GPL')
groups=('unity-extra')
depends=('glib2' 'gtk3' 'libunity-webapps' 'unity-asset-pool' 'webkitgtk3' 'xdg-utils')
makedepends=('intltool')
optdepends=('gjs: Development (or something)'
            'python-polib: I have no idea')
install=webapps-applications.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/webapps-applications_${_actual_ver}${_extra_ver}.orig.tar.gz")
sha512sums=('6f61fc9c20bdc859bbfaea8e33ccfeea65ec0a31452d6fa311dd6bdeab2d9893a5b07c48c6e8d8c697c02ced3f10568089a983b10a47946771e4218e9bd43d58')

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  autoreconf -vfi

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  make DESTDIR="${pkgdir}/" install

  install -dm755 "${pkgdir}/usr/share/unity-webapps/tools/"
  install -m644 scripts/po2json.py \
                scripts/applinter.sh \
                scripts/style_checker.js \
                scripts/third_party/jslint.js \
                "${pkgdir}/usr/share/unity-webapps/tools/"
  install -m644 common/rules.mk "${pkgdir}/usr/share/unity-webapps/"
}

# vim:set ts=2 sw=2 et:
