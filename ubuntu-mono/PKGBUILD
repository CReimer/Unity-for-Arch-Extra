# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=ubuntu-mono
pkgver=0.0.42
pkgrel=100
pkgdesc="Icons for the panel, designed in a simplified monochrome style"
arch=('any')
url="https://launchpad.net/ubuntu-mono"
license=('CCPL')
groups=('unity-extra')
depends=('humanity-icon-theme' 'gnome-icon-theme' 'hicolor-icon-theme')
makedepends=('python2' 'imagemagick')
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${pkgver}.tar.gz")
sha512sums=('520eb4b8d6a2134e9a7fc39258929f0a07b090599c1165292451b5a2512be88dc8c3dbb4e2873bbe9c6804b807cd163593bfd007827d2a7999d7e0d641de98d8')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Python 2 fix
  sed -i 's|^\(#!.*python$\)|\12|g' scavenge.py

  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Install icons
  install -dm755 "${pkgdir}/usr/share/icons/"
  cp -av ubuntu-mono-dark "${pkgdir}/usr/share/icons/"
  cp -av ubuntu-mono-light "${pkgdir}/usr/share/icons/"
  cp -av LoginIcons "${pkgdir}/usr/share/icons/"

  for i in "${pkgdir}"/usr/share/icons/ubuntu-mono-*; do
    cp -avt ${i} debian/places/
  done
}

# vim:set ts=2 sw=2 et:
