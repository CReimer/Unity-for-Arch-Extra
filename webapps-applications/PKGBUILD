# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=webapps-applications
_actual_ver=2.4.16
_extra_ver=+13.10.20130814
pkgver=${_actual_ver}daily13.08.14
pkgrel=100
pkgdesc="Unity WebApps integration scripts"
arch=('any')
url="https://launchpad.net/webapps-applications"
license=('GPL')
groups=('unity-extra')
depends=('glib2' 'gtk3' 'libunity-webapps' 'unity-asset-pool' 'webkitgtk3' 'xdg-utils')
makedepends=('intltool')
optdepends=('gjs: Development (or something)'
            'python-polib: I have no idea')
install=webapps-applications.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/webapps-applications_${_actual_ver}${_extra_ver}.orig.tar.gz")
sha512sums=('c399ede6c1614581d913463531037ceaa443b8bca395c827c445572e2140814569eacb4cb4105286e034e6a93fc807de9f5b29ec9e351fa5f39d1b5d7070a2c0')

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  autoreconf -vfi

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  make DESTDIR="${pkgdir}/" install

  install -dm755 "${pkgdir}/usr/share/unity-webapps/tools/"
  install -m644 scripts/po2json.py \
                scripts/applinter.sh \
                scripts/style_checker.js \
                scripts/third_party/jslint.js \
                "${pkgdir}/usr/share/unity-webapps/tools/"
  install -m644 common/rules.mk "${pkgdir}/usr/share/unity-webapps/"
}

# vim:set ts=2 sw=2 et:
