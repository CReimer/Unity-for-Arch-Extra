# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=webapps-applications
pkgver=2.4.13
pkgrel=100
pkgdesc="Unity WebApps integration scripts"
arch=('any')
url="https://launchpad.net/webapps-applications"
license=('GPL')
groups=('unity-extra')
depends=('libunity-webapps' 'xdg-utils')
makedepends=('python-polib' 'intltool')
install=webapps-applications.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/webapps-applications_${pkgver}.orig.tar.gz")
sha512sums=('2f2eacaa37a1ebc2f97c1d05d885b2ab5d3eccf59bd124533d8617f67159b15a917578eb83ce340baf7f6ad92e65a6e1dbebb7473c832275a1996f2cf67876ac')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  autoreconf -vfi
  intltoolize -f

  ./configure --prefix=/usr --enable-default-applications
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install

  install -dm755 "${pkgdir}/usr/bin/"
  install -m755 scripts/install-default-webapps-in-launcher.py \
    "${pkgdir}/usr/bin/webapps-add-defaults-to-launcher"

  # What is heck is zn_CN? zh stands for the first two letter of the pinyin of
  # 中文 (zhongwen)
  mv "${pkgdir}"/usr/share/locale/{zn,zh}_CN/
}

# vim:set ts=2 sw=2 et:
