# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgbase=webaccounts-browser-extension
pkgname=('webaccounts-browser-extension' 'chromium-extension-webaccounts' 'firefox-extension-webaccounts')
_ubuntu_rel=0ubuntu4
pkgver=0.4.5.${_ubuntu_rel}
pkgrel=101
pkgdesc="Ubuntu Online Accounts extensions for Firefox and Chromium"
arch=('i686' 'x86_64')
url="https://launchpad.net/webaccounts-browser-extension"
license=('GPL')
groups=('unity-extra')
depends=('json-glib' 'libaccounts-glib' 'openssl')
makedepends=('intltool' 'unzip' 'vim' 'zip')
extrafiles=('webaccounts-browser-extension.install')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/webaccounts-browser-extension_${pkgver%.*}.orig.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/webaccounts-browser-extension_${pkgver%.*}-${_ubuntu_rel}.debian.tar.gz")
sha512sums=('45d9e933d5da0d7d077dbb5c3dea409ea289e51ef01f3143f5052be957ad36576c890b8aab8d4ff8221d4e2e4f973519fba24bc18f24c66ae016b7e4e2a30dfa'
            'c6c351db4668c5eed11eca891356391261313c1b4e33819ce276b7b71be559db9812e34a2d0ed11f29d7fcc7d31e10a3f9952f0b70578176ab7caf75ca27b247')

build() {
  cd "${srcdir}/webaccounts-browser-extension-${pkgver%.*}"

  cp "${srcdir}/debian/webaccounts.pem" .

  autoreconf -vfi

  ./configure --prefix=/usr
  make
}

package_webaccounts-browser-extension() {
  pkgdesc="Common files for the Firefox and Chromium webaccounts extensions"
  options=('!libtool')
  install=webaccounts-browser-extension.install

  cd "${srcdir}/webaccounts-browser-extension-${pkgver%.*}"
  make DESTDIR="${pkgdir}/" install

  rm -rv "${pkgdir}/usr/lib/webaccounts-chromium/"
  rm -rv "${pkgdir}/usr/lib/webaccounts-firefox/"
  rm -rv "${pkgdir}/usr/share/chromium/"
}

package_chromium-extension-webaccounts() {
  pkgdesc="Ubuntu Online Accounts extension for Chromium"
  depends=("webaccounts-browser-extension=${pkgver}" 'chromium' 'credentials-preferences')

  cd "${srcdir}/webaccounts-browser-extension-${pkgver%.*}"

  make -C chromium-extension DESTDIR="${pkgdir}/" install
}

package_firefox-extension-webaccounts() {
  pkgdesc="Ubuntu Online Accounts extension for Firefox"
  depends=("webaccounts-browser-extension=${pkgver}" 'firefox' 'credentials-preferences')

  cd "${srcdir}/webaccounts-browser-extension-${pkgver%.*}"

  local emid=$(sed -n 's/.*<em:id>\(.*\)<\/em:id>.*/\1/p' \
    firefox-extension/install.rdf | head -1)
  install -dm755 "${pkgdir}/usr/lib/firefox/extensions/${emid}/"
  unzip firefox-extension/webaccounts-firefox-extension.xpi -d \
    "${pkgdir}/usr/lib/firefox/extensions/${emid}/"
}

# vim:set ts=2 sw=2 et:
