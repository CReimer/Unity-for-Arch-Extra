# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=ubuntu-wallpapers
_actual_ver=13.04.0
_extra_ver=+13.10.20130710
_ubuntu_rel=0ubuntu1
# AUR is broken once again. How is ${_actual_ver}.${_extra_ver} greater than
# 32 characters?
pkgver=13.10
pkgrel=1
pkgdesc="The default wallpapers for Ubuntu (from 9.10 Karmic Koala to 13.10 Saucy Salamander)"
arch=('any')
url="https://launchpad.net/ubuntu-wallpapers"
license=('GPL')
groups=('unity-extra')
makedepends=('libxslt' 'python2-distutils-extra')
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${_actual_ver}${_extra_ver}.orig.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.diff.gz")
sha512sums=('283a917395b8d895b270df0ac91472e721d0479c1ab0723dc4fb2c0b3acd07d0e5fec77edc64d600539355ce3f296258537c3f41b6ea92e9681ad21811fbf2f3'
            'd08d65dc4de1369d8e0960ac32c12dc1a1b1160551196d9ca1cb80aed04a257192511f857a4a1d0e4132f939e14b23cef1d150e9715a98653a452417b228e92d')

prepare() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  # The setup.py script reads the package version from debian/*
  patch -p1 -i "${srcdir}/${pkgname}_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.diff"
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  python2 setup.py install --root="${pkgdir}/" --optimize=1
}

# vim:set ts=2 sw=2 et:
