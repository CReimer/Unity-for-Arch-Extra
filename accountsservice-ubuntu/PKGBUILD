# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=accountsservice-ubuntu
_ppa_ver=0.6.30
_ppa_rel=0ubuntu1+logind~raring2
pkgver=0.6.31
pkgrel=100
pkgdesc="D-Bus interface for user account query and manipulation"
arch=('i686' 'x86_64')
url="http://cgit.freedesktop.org/accountsservice/"
license=('GPL3')
depends=('glib2' 'polkit' 'systemd' 'lightdm-ubuntu')
makedepends=('intltool' 'gobject-introspection' 'vala')
provides=("accountsservice=${pkgver%.*}")
conflicts=('accountsservice')
options=('!libtool')
source=("http://cgit.freedesktop.org/accountsservice/snapshot/accountsservice-${pkgver}.tar.xz"
        "http://ppa.launchpad.net/gnome3-team/gnome3-staging/ubuntu/pool/main/a/accountsservice/accountsservice_${_ppa_ver}-${_ppa_rel}.debian.tar.gz"
        'more-exclusions.patch')
sha512sums=('b216f4c8ae58e35931f3482b2c36aef2ac89bc8fc9d754e2deaf4ed1bc0a4a3d20eae0600c5c1e54d1bb06b292e2eda2d17a25e8f1644b076c766c664fddfcf1'
            '5b01b18b02478793c50fbfb825d275cca3c1a4d5d902ad0e78f51346345e9f25084156d0726620df68822d4aecfede87503739060278867b1b16840d2928e9e6'
            '79d381537320c0ef75ece3db213be7e045caafc9443ac3d368d6819219b3a53009e3dc1d3d6201f220a8b343ce67ddb6b49b5351d3136d237b01066af6080ddf')

prepare() {
  cd "${srcdir}/accountsservice-${pkgver}"

  # Apply Arch Linux patches
  patch -p1 -i "${srcdir}/more-exclusions.patch"

  # Apply Ubuntu patches
    # Disable patches
      # Arch Linux uses wheel group for system administration
        sed -i '/0002-create-and-manage-groups-like-on-a-debian-system.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
        sed -i '/0002-create-and-manage-groups-like-on-a-ubuntu-system.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
      # Arch Linux uses /etc/gdm as the GDM configuration directory
        sed -i '/0005-gdm_config_file_path.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
        sed -i '/0005-gdm_config_file_path_ubuntu.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
      # adduser is Debian-specific
        sed -i '/0006-adduser_instead_of_useradd.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
      # Language patches are still pretty specific to Ubuntu (Ubuntu is trying
      # to get them merged upstream)
        sed -i '/0001-formats-locale-property.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
        sed -i '/0009-language-tools.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
        sed -i '/0010-set-language.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
        sed -i '/0012-add-keyboard-layout-support.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
      # systemd can be used in Arch Linux
        sed -i '/2002-disable_systemd.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
      # This is not the proper way to fix the issue: run "intltoolize -f"
        sed -i '/1001-buildsystem.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
      # Does not apply cleanly
        sed -i '/0013-add-has-message-support.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
        sed -i '/2001-filtering_out_users.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
    # Fix patches
      #sed -i 's,/etc/gdm3/daemon\.conf,/etc/gdm/custom.conf,g' \
      #  "${srcdir}"/debian/patches/2001-filtering_out_users.patch

  for i in $(grep -v '#' "${srcdir}/debian/patches/ubuntu.series"); do
    msg "Applying ${i} ..."
    patch -p1 -i "${srcdir}/debian/patches/${i}"
  done
}

build() {
  cd "${srcdir}/accountsservice-${pkgver}"

  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/accountsservice \
    --with-systemdsystemunitdir=/usr/lib/systemd/system \
    --localstatedir=/var \
    --disable-static \
    --enable-systemd

  make
}

package() {
  cd "${srcdir}/accountsservice-${pkgver}"

  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
