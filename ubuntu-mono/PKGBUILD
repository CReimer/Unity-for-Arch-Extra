# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=ubuntu-mono
pkgver=0.0.45
pkgrel=100
pkgdesc="Icons for the panel, designed in a simplified monochrome style"
arch=('any')
url="https://launchpad.net/ubuntu-mono"
license=('CCPL')
groups=('unity-extra')
depends=('humanity-icon-theme' 'gnome-icon-theme' 'hicolor-icon-theme')
makedepends=('python2' 'imagemagick')
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${pkgver}.tar.gz")
sha512sums=('270ec99fed22ec5090210d1bf74b598d8199368fec2a8f5ac4cf26843503e476d7164224c0cb46ddcdfc38da28deab863ccd01b70ffe339414da23b0868a403e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Python 2 fix
  sed -i 's|^\(#!.*python$\)|\12|g' scavenge.py

  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Install icons
  install -dm755 "${pkgdir}/usr/share/icons/"
  cp -av ubuntu-mono-dark "${pkgdir}/usr/share/icons/"
  cp -av ubuntu-mono-light "${pkgdir}/usr/share/icons/"
  cp -av LoginIcons "${pkgdir}/usr/share/icons/"

  for i in "${pkgdir}"/usr/share/icons/ubuntu-mono-*; do
    cp -avt ${i} debian/places/
  done
}

# vim:set ts=2 sw=2 et:
