# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=empathy-ubuntu
_ppa_rel=0ubuntu1~saucy3
pkgver=3.10.1
pkgrel=100
pkgdesc="A GNOME instant messaging client using the Telepathy framework."
arch=('i686' 'x86_64')
url="http://live.gnome.org/Empathy"
license=('GPL2')
provides=("empathy=${pkgver}")
conflicts=('empathy')
depends=('clutter-gst' 'clutter-gtk' 'folks' 'gcr' 'iso-codes' 'libcanberra' 'libpulse' 'webkitgtk3' 'telepathy-farstream' 'telepathy-glib' 'telepathy-logger' 'telepathy-mission-control' 'geoclue2' 'geocode-glib' 'cheese' 'libchamplain' 'libunity' 'ido' 'unity-asset-pool')
makedepends=('intltool' 'itstool' 'docbook-xsl' 'yelp-tools' 'python2')
optdepends=('telepathy-gabble: XMPP/Jabber support'
            'telepathy-idle: IRC support'
            'telepathy-salut: Link-local XMPP support'
            'telepathy-rakia: SIP support'
            'telepathy-haze: libpurple support')
options=('!makeflags')
groups=('gnome')
install=empathy.install
source=("http://ftp.gnome.org/pub/GNOME/sources/${pkgname%-*}/${pkgver%.*}/${pkgname%-*}-${pkgver}.tar.xz"
        "http://ppa.launchpad.net/gnome3-team/gnome3-staging/ubuntu/pool/main/e/empathy/empathy_${pkgver}-${_ppa_rel}.debian.tar.gz")
sha512sums=('34d2e9b13af26e86feedbdaf50e2b546e1d14d433043c7659a3a4b695d05c5650cc3834d926086db705ce1248965bafe166e7193de1518bb208d205e17e70abd'
            '831c28e2ce9e8a2ff43f4f53ea32bb4d078632b06a6fb8bc838f307980336a88dd36b653754aa7769fb16adf0ddf6e340032c1f13e83b418da5012dcfdd882cb')

prepare() {
  cd "${pkgname%-*}-${pkgver}"

  # Apply Ubuntu's patches
    # Disable patches
      # Do not use Ubuntu Online Accounts (broken with GNOME 3.10)
        sed -i '/ubuntu_launch_uoa_panel.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/00_linker-fixes.patch/d' "${srcdir}/debian/patches/series"

  for i in $(grep -v '#' "${srcdir}/debian/patches/series"); do
    patch -p1 -i "${srcdir}/debian/patches/${i}"
  done
}

build() {
  cd "${pkgname%-*}-${pkgver}"

  #autoreconf -vfi

  PYTHON=python2 ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc  \
    --libexecdir=/usr/lib/empathy \
    --disable-static \
    --disable-schemas-compile \
    --enable-gst-1.0=yes \
    --enable-spell=yes \
    --enable-webkit=yes \
    --enable-map=no \
    --enable-location=no \
    --enable-geocode=no \
    --enable-gudev=yes \
    --enable-call-logs=yes \
    --enable-call=yes \
    --enable-ubuntu-online-accounts=no \
    --enable-goa=yes \
    --enable-libunity=yes \
    --enable-control-center-embedding=yes \
    --with-connectivity=nm

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make -j1
}

package() {
  cd "${pkgname%-*}-${pkgver}"
  make -j1 DESTDIR="${pkgdir}" install

  install -dm755 "${pkgdir}/usr/share/indicators/messages/applications/"
  install -m644 "${srcdir}/debian/indicators/empathy" \
    "${pkgdir}/usr/share/indicators/messages/applications/"
}
