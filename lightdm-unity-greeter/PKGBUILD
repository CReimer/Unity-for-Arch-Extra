# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Taken from M0Rf30 morf3089 at gmail dot com

pkgname=lightdm-unity-greeter
_ubuntu_rel=0ubuntu1
pkgver=13.04.2
pkgrel=103
pkgdesc="The greeter (login screen) application for Unity. It is implemented as a LightDM greeter."
arch=('i686' 'x86_64')
url="https://launchpad.net/unity-greeter"
license=('GPL3' 'LGPL3')
groups=('unity-extra')
depends=('gnome-doc-utils' 'lightdm-ubuntu' 'gnome-common' 'libindicator3' 'cantarell-fonts' 'light-themes' 'ubuntu-wallpapers' 'ttf-ubuntu-font-family')
makedepends=('gnome-settings-daemon' 'pkg-config' 'vala' 'archlinux-artwork' 'imagemagick' 'librsvg' 'intltool')
options=('!libtool')
install=unity-greeter.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname#*-}_${pkgver}-${_ubuntu_rel}.diff.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname#*-}_${pkgver}.orig.tar.gz"
        '10-unity.defaults'
        '50-unity-greeter.rules'
        '0001_autoconf.patch')
sha512sums=('b0dab25a74b88cf986278fae8c3c38c0901107fefbb77693c41f804a885c6bbba4fa16f623b4aac4e0b6af572abeb0271859c199414ac58c1785147e6af2e420'
            '3b09d6612ef2fdc5537ba5392c99916e70de4b725f0b0ba68c4a27f3f95b023f8da57992214e56311424e678e5fbaab7280b7c3cedf82119e33db4858af229db'
            'ee5d1f17dddd99ba55bbaca8aff5e8487c9b9f7e1eef570d2adb3d2519e19ef437b160414468ef85a8c1b14af1eee23c714e2086291701edfbab2f43064e2cd7'
            '5d0f1b8221dfe02670df3ba88011dc0ce744bfd4205a9900dd8096de222358a740a384a47f13ed4e5b94e24d4a8ff5639117464c61a689dd7bd9025a0900f529'
            'c31d52509484f26361de343f0bd53cba97ebe55ec7f028ec2afa166fec2f6d073e172788b5fcb2cd9bf7f4f2bab4a1f2268d30c3cefbbbd3e62948f64d0b1272')

prepare() {
  cd "${srcdir}/${pkgname#*-}-${pkgver}"

  # Apply Ubuntu patches
  patch -p1 -i "${srcdir}"/${pkgname#*-}_${pkgver}-${_ubuntu_rel}.diff
  for i in $(grep -v '#' debian/patches/series); do
    patch -p1 -i "debian/patches/${i}"
  done

  patch -p1 -i "${srcdir}/0001_autoconf.patch"

  # Use Arch Linux logo (retain the same 245px height as the Ubuntu logo)
  rm data/logo.png
  convert -background none -resize 245 \
    /usr/share/archlinux/logos/archlinux-mono-white.svg \
    data/logo.png
}

build() {
  cd "${srcdir}/${pkgname#*-}-${pkgver}"

  # Link against libm and libx11
  export CFLAGS="${CFLAGS} -lm -lX11"

  autoreconf -vfi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/lightdm

   make
}

package() {
  cd "${srcdir}/unity-greeter-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # Install PolicyKit file for allowing the lightdm user to use NetworkManager
  # Note: PolicyKit no longer reads pkla files after version 0.107, so Ubuntu's
  # policy file won't work.
  install -dm700 "${pkgdir}/usr/share/polkit-1/rules.d/"
  install -m644 "${srcdir}/50-unity-greeter.rules" \
    "${pkgdir}/usr/share/polkit-1/rules.d/"
  #install -dm700 "${pkgdir}/var/lib/polkit-1/"
  #install -dm755 "${pkgdir}/var/lib/polkit-1/localauthority/10-vendor.d/"
  #install -m644 "${srcdir}/debian/unity-greeter.pkla" \
  #  "${pkgdir}/var/lib/polkit-1/localauthority/10-vendor.d/"

  # Install default GSettings/dconf settings for the guest account (requires
  # lightdm-ubuntu).
  install -dm755 "${pkgdir}/etc/guest-session/gsettings/"
  install -m644 "${srcdir}/10-unity.defaults" \
    "${pkgdir}/etc/guest-session/gsettings/"
}

# vim:set ts=2 sw=2 et:
