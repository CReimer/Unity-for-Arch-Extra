# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=plasma-widget-menubar
pkgver=0.2.0
pkgrel=100
pkgdesc="A Plasma widget to display menubar of application windows"
arch=('i686' 'x86_64')
url="https://launchpad.net/plasma-widget-menubar"
license=('GPL')
depends=('kdelibs' 'qjson' 'libdbusmenu-qt')
makedepends=('cmake' 'automoc4')
optdepends=('appmenu-gtk: support for GTK+ apps'
            'appmenu-gtk3: support for GTK+ 3 apps'
            'appmenu-qt: support for Qt apps')
groups=('unity')
source=("http://launchpad.net/${pkgname}/trunk/${pkgver}/+download/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('73a675a170e53dfc5a8471f879f8b25ead59e3dc2ef30e87777c80994594d3849bca8e139679d33df77ec317fa0467590fb3a4c5b5d0c395ab89a0cc9158df8f')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  mkdir build
  cd build
  cmake .. -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr \
           -DAutomoc4_DIR=/usr/lib/automoc4
  make ${MAKEFLAGS}
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}/build"
  make -C tests check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/build"

  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
