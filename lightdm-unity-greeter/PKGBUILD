# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Taken from M0Rf30 morf3089 at gmail dot com

pkgname=lightdm-unity-greeter
_ubuntu_rel=0ubuntu3
pkgver=0.2.3.${_ubuntu_rel}
pkgrel=100
pkgdesc="The greeter (login screen) application for Unity. It is implemented as a LightDM greeter."
arch=('i686' 'x86_64')
url="https://launchpad.net/unity-greeter"
license=('GPL3' 'LGPL3')
groups=('unity-extra')
depends=('gnome-doc-utils' 'lightdm-ubuntu' 'gnome-common' 'libindicator' 'cantarell-fonts' 'light-themes' 'ubuntu-wallpapers' 'ttf-ubuntu-font-family')
makedepends=('gnome-settings-daemon' 'pkg-config' 'bzr' 'vala-git' 'autoconf')
options=(!libtool)
install=unity-greeter.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname#*-}_${pkgver%.*}-${_ubuntu_rel}.debian.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname#*-}_${pkgver%.*}.orig.tar.gz")
sha512sums=('42e0230e9fa815dcb0fdb1e0400d064864c0ff2c0db7bf3e42e35b72bc79a94f73f8225d79d808446e91e68175d607be12c9c616bc0ce8cd89952514f4fe91a1'
            'cbfc849931b700da024505bfe4a38faa91d55ceba699c01d21a89dd8a59fca39e0efdb68acd4dedeb923e1b03d323ebae2fb855a0f7507c974f9e5b1c92d8d6d')

build() {
  cd "${srcdir}/${pkgname#*-}-${pkgver%.*}"

  # Apply Ubuntu patches
  for i in $(cat "${srcdir}/debian/patches/series" | grep -v '#'); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  # Link against libm and libx11
  CFLAGS="${CFLAGS} -lm -lX11"

  autoreconf -vfi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/lightdm

   make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/unity-greeter-${pkgver%.*}"

  make DESTDIR="${pkgdir}" install
}

