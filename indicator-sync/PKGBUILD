# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Written by: roi.dm <https://bbs.archlinux.org/viewtopic.php?pid=1177835#p1177835>

pkgname=indicator-sync
_actual_ver=12.10.5
_extra_ver=daily13.03.28.1
pkgver=${_actual_ver}${_extra_ver}
pkgrel=100
pkgdesc="Sync menu to aggregate services performing background synchronization"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-sync"
license=('GPL')
depends=('libdbusmenu-gtk3' 'ido' 'libindicator3')
makedepends=('intltool')
groups=('unity')
options=('!libtool')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/indicator-sync_${_actual_ver}${_extra_ver}.orig.tar.gz"
        'revert_r30.patch')
sha512sums=('e67f15f112001094838da6d067a02d8c2cf8312a57482f2a090c01d858bc26296b7063c6160a6db1601b0ffa4e294de718458afdec2a8e37c395217db76993a5'
            'caa8751368f0d027c5326d89443d113538861ce41d4d5823bfc036ef2dcfaf9e705d13a18d46fec8b8f03e5f389ac6403ecb82005fb106c797ab426ccf79912c')

prepare() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  patch -p0 -i "${srcdir}/revert_r30.patch"
}

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  export CFLAGS="${CFLAGS} -Wno-error"

  autoreconf -vfi

  ./configure --prefix=/usr --libexecdir=/usr/share/${pkgname} --disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
