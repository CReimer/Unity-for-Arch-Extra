# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgbase=python-pyside
pkgname=('python-pyside' 'python2-pyside')
pkgver=1.2.1
pkgrel=100
pkgdesc="Python bindings for Qt"
arch=('i686' 'x86_64')
url="http://www.pyside.org/"
license=('LGPL')
groups=('unity-extra')
makedepends=('automoc4' 'cmake' 'libxslt' 'mesa' 'python' 'python2' 'python-shiboken' 'python2-shiboken' 'phonon' 'qt4' 'qtwebkit')
source=("https://distfiles.macports.org/py-pyside/pyside-qt4.8+${pkgver}.tar.bz2")
sha512sums=('674a0d86be311650b22a9915a91f25e70b0a739d9c2ad92688ec4c624182ecf5184cbc761ea014ca6955b4bc9338acda2371eeffa302afc3873b95b88dea4b2b')

build() {
  cd "${srcdir}/pyside-qt4.8+${pkgver}"

  mkdir build-python2 build-python3

  pushd build-python2/
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DPYTHON_SUFFIX=-python2.7 \
    -DDEFAULT_PYTHON_BASENAME=-python2.7 \
    -DQT_QMAKE_EXECUTABLE=qmake-qt4 \
    -DQT_PHONON_INCLUDE_DIR=/usr/include/qt4/phonon

  make
  popd

  pushd build-python3/
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DQT_QMAKE_EXECUTABLE=qmake-qt4 \
    -DQT_PHONON_INCLUDE_DIR=/usr/include/qt4/phonon

  make
  popd
}

package_python-pyside() {
  cd "${srcdir}/pyside-qt4.8+${pkgver}/build-python3/"

  make DESTDIR="${pkgdir}/" install
}

package_python2-pyside() {
  cd "${srcdir}/pyside-qt4.8+${pkgver}/build-python2/"

  make DESTDIR="${pkgdir}/" install

  mv "${pkgdir}"/usr/include/PySide{,-py2}
  mv "${pkgdir}"/usr/lib/cmake/PySide-${pkgver}/PySideConfig{,-py2}.cmake
  mv "${pkgdir}"/usr/lib/cmake/PySide-${pkgver}/PySideConfigVersion{,-py2}.cmake
  mv "${pkgdir}"/usr/lib/pkgconfig/pyside{,-py2}.pc
  mv "${pkgdir}"/usr/share/PySide{,-py2}
}

# vim:set ts=2 sw=2 et:
