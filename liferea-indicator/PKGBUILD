# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Otiginal Maintainer: brainpower <brainpower@gulli.com>
# Original Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: Eric Bélanger <eric@archlinux.org>

_pkgname=liferea
pkgname=liferea-indicator
pkgver=1.8.7
pkgrel=100
pkgdesc="A desktop news aggregator for online news feeds and weblogs with Messages Indicator support"
arch=('i686' 'x86_64')
url="http://liferea.sourceforge.net/"
license=('GPL')
depends=('gconf' 'libwebkit' 'libnotify' 'libunique' 'json-glib' 'libindicate-gtk2' 'hicolor-icon-theme')
makedepends=('intltool')
provides=("liferea=$pkgver")
conflicts=('liferea')
options=('!libtool' '!emptydirs')
install=liferea.install
source=("http://downloads.sourceforge.net/sourceforge/liferea/liferea-${pkgver}.tar.gz"
        'libindicate-fix.patch')
sha1sums=('3cdadae3b3bcec2da048257b72a5d7c11a9621f1'
          '217d72ecdcc0cf15b9799311064c642058d6a7c1')


build() {
  cd "${srcdir}/liferea-${pkgver}"

  patch -Np1 -i "${srcdir}/libindicate-fix.patch"
  sed -i -e 's/indicate-0.5/indicate-0.7/' \
         -e 's/indicate-gtk-0.5/indicate-gtk-0.7/' configure

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --disable-schemas-install --enable-libindicate
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/liferea-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -dm755 "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" "${pkgdir}"/etc/gconf/schemas/*.schemas
  rm -v "${pkgdir}"/etc/gconf/schemas/*.schemas

  install -dm755 "${pkgdir}/usr/share/indicators/messages/applications/"
  echo '/usr/share/applications/liferea.desktop' > "${pkgdir}/usr/share/indicators/messages/applications/liferea"
}

# vim:set ts=2 sw=2 et:
