# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Otiginal Maintainer: brainpower <brainpower@gulli.com>
# Original Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: Eric Bélanger <eric@archlinux.org>

pkgname=liferea-indicator
_ubuntu_rel=1ubuntu1
pkgver=1.10.3
pkgrel=1
pkgdesc="A desktop news aggregator for online news feeds and weblogs with Messages Indicator support"
arch=('i686' 'x86_64')
url="http://liferea.sourceforge.net/"
license=('GPL')
depends=('webkitgtk' 'libnotify' 'json-glib' 'libpeas' 'python2-gobject' 'gnome-icon-theme' 'desktop-file-utils' 'indicator-messages' 'libunity')
makedepends=('intltool')
provides=("liferea=${pkgver}")
conflicts=('liferea')
options=('!libtool' '!emptydirs')
install=liferea.install
source=("http://downloads.sourceforge.net/sourceforge/liferea/liferea-${pkgver}.tar.bz2"
        "https://launchpad.net/ubuntu/+archive/primary/+files/liferea_${pkgver}-${_ubuntu_rel}.debian.tar.xz")
sha512sums=('4df1f8849ecc4bfc3e9de3afcaca65447f1ad7f19bd707790df5b8437e1f9c095ecba3f9251ea5f3bff880f6bb9f01822634f0c37688fddc32203f12cf5c53eb'
            'd7bbc1c7075f416eb95097336e66d7f732c54caa221ea693d4c0d991e8d7d89e680e6580f79436a4f8b4efad1a75440f86ef6a4cf673223d938b417417e02a02')

prepare() {
  cd "${srcdir}/liferea-${pkgver}"

  # Apply Ubuntu's patches
    # Disable patches
      sed -i '/www-browser.patch/d' "${srcdir}/debian/patches/series"
  for i in $(grep -v '#' "${srcdir}/debian/patches/series"); do
    patch -p1 -i "${srcdir}/debian/patches/${i}"
  done
}

build() {
  cd "${srcdir}/liferea-${pkgver}"

  autoreconf -vfi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-schemas-compile \
    --enable-nm \
    --enable-gnutls \
    --enable-lua \
    --enable-libnotify \
    --enable-messaging-menu

  make
}

package() {
  cd "${srcdir}/liferea-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
