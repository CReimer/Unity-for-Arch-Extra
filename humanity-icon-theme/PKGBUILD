# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=humanity-icon-theme
pkgver=0.6.1
pkgrel=100
pkgdesc="Humanity Icon Theme"
arch=('any')
url="https://launchpad.net/humanity"
license=('GPL')
groups=('unity-extra')
depends=('gnome-icon-theme' 'hicolor-icon-theme')
makedepends=('icon-naming-utils')
provides=("humanity-icons=${pkgver}")
conflicts=('humanity-icons')
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${pkgver}.tar.gz")
sha512sums=('3fd0d87bbca4d7d4b5790cdec530a8c265af56e0a2a506d04e4c528c40a0edd2e870e8d21ad9e60d65a7ff0ac3098813d3b7cb238a9d44086bda21219ff3e20e')

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Install Humanity icon theme
  find Humanity -type f -exec install -Dm644 {} "${pkgdir}"/usr/share/icons/{} \;

  # Install Humanity-Dark icon theme
  find Humanity-Dark -type f -exec install -Dm644 {} "${pkgdir}"/usr/share/icons/{} \;

  # From debian/rules
  for i in $(find "${pkgdir}/usr/share/icons/" -mindepth 2 -maxdepth 2 -type d); do
    (
    cd "${i}"
    for j in *; do
      /usr/lib/icon-naming-utils/icon-name-mapping -c "${j}"
    done
    )
  done
}

# vim:set ts=2 sw=2 et:
