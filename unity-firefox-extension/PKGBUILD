# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# Upstream package name is unity-firefox-extension

pkgname=firefox-extension-unity
_actual_ver=2.4.2
_ubuntu_rel=0ubuntu1
pkgver=${_actual_ver}.${_ubuntu_rel}
pkgrel=100
pkgdesc="Firefox extension for Unity integration"
arch=('i686' 'x86_64')
url="https://launchpad.net/unity-firefox-extension"
license=('GPL')
groups=('unity-extra')
depends=('firefox-extension-websites-integration' 'libunity-webapps')
makedepends=('gtk2' 'libxslt' 'zip' 'unzip')
options=('!libtool')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/unity-firefox-extension_${_actual_ver}.orig.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/unity-firefox-extension_${_actual_ver}-${_ubuntu_rel}.debian.tar.gz"
        'launcher_bfb.png')
sha512sums=('c7879355ffdb5f2c139772483bac25bac08aa9b31565be9a5d27e6f03e37c61dafad205d70d0bbc1a25f707852143bb999a9af63c62ece7e14889d2ba841c1b6'
            'd449b53b590ad0d503d78c6e1b8be806cb1acb77fd2fdb41f464942f96837778e8ccc9b3d9cd24a4294895ab56a4940bf12f90401532790b7fe1dfa1d0909836'
            '9f5469c0300d0c33e26db670771803efd3d821e82689fbb1391a31b8fd0e280a12ea010fe6f32a702a5d1797918d416c8adabfccf6049e43b496ebb6dbe2c054')

build() {
  cd "${srcdir}/unity-firefox-extension-${_actual_ver}"

  # Apply Ubuntu's patches
  for i in $(grep -v '#' "${srcdir}/debian/patches/series"); do
    patch -p1 -i "${srcdir}/debian/patches/${i}"
  done

  pushd libufe-xidgetter/
  autoreconf -vfi
  ./configure --prefix=/usr --disable-static
  make ${MAKEFLAGS}
  popd

  pushd unity-firefox-extension/
  bash ./build.sh
  popd
}

package() {
  cd "${srcdir}/unity-firefox-extension-${_actual_ver}"

  pushd libufe-xidgetter/
  make DESTDIR="${pkgdir}/" install
  popd

  pushd unity-firefox-extension/
  local emid=$(sed -n 's/.*<em:id>\(.*\)<\/em:id>.*/\1/p' install.rdf | head -1)
  install -dm755 "${pkgdir}/usr/lib/firefox/extensions/${emid}/"
  unzip unity.xpi -d "${pkgdir}/usr/lib/firefox/extensions/${emid}/"
  popd

  # Arch Linux logo
  rm "${pkgdir}/usr/lib/firefox/extensions/${emid}/skin/cof.png"
  install -m644 "${srcdir}/launcher_bfb.png" \
    "${pkgdir}/usr/lib/firefox/extensions/${emid}/skin/cof.png"
}

# vim:set ts=2 sw=2 et:
