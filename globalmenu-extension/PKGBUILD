# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# Based on Tom Kuther's PKGBUILD

_bzr_rev=523

pkgbase=globalmenu-extension
pkgname=('firefox-globalmenu' 'thunderbird-globalmenu')
pkgver=3.9
pkgrel=2
pkgdesc="Global Menu Extensions for Firefox and Thunderbird"
arch=('i686' 'x86_64')
url="https://launchpad.net/globalmenu-extension"
license=('GPL' 'LGPL' 'MPL')
makedepends=('bzr' 'autoconf2.13' 'libidl2' 'unzip' 'xulrunner' 'yasm' 'zip' 'libdbusmenu-gtk2' 'mesa' 'libnotify')
source=("${pkgbase}::bzr+http://bazaar.launchpad.net/~extension-hackers/globalmenu-extension/trunk/#${_bzr_rev}")
sha512sums=('SKIP')

build() {
  cd "${srcdir}/${pkgbase}"

  autoconf-2.13
  sed -i 's/^ \t/\t/g' config/rules.mk
  sed -i '/^XPIDL_COMPILE[[:space:]]*=/s@\$(LIBXUL_DIST)/@&sdk/@' config/config.mk

  export CPPFLAGS="${CPPFLAGS} -O2"

  ./configure --with-libxul-sdk=`pkg-config --variable=sdkdir libxul` \
    --with-system-libxul \
    --with-system-nspr \
    --with-system-nss \
    --enable-application=extensions \
    --enable-extensions=globalmenu \
    --disable-tests \
    --disable-necko-wifi \
    --disable-crashreporter \
    --disable-ogg \
    --disable-alsa \
    --disable-webgl \
    --disable-webm \
    --disable-webrtc \
    --disable-dash
  make
}

package_firefox-globalmenu() {
  cd "${srcdir}/${pkgbase}"
  pkgdesc="Global Menu Extension for Firefox"
  depends=('firefox' 'libdbusmenu-gtk2' 'libnotify')

  emid=$(sed -n '/.*<em:id>\(.*\)<\/em:id>.*/{s//\1/p;q}' \
         extensions/globalmenu/package/install.rdf)
  install -dm755 "${pkgdir}/usr/lib/firefox/extensions/${emid}/"
  unzip -d "${pkgdir}/usr/lib/firefox/extensions/${emid}/" \
           dist/xpi-stage/globalmenu.xpi
}

package_thunderbird-globalmenu() {
  cd "${srcdir}/${pkgbase}"
  pkgdesc="Global Menu Extension for Thunderbird"
  depends=('libdbusmenu-gtk2' 'thunderbird' 'libnotify')

  emid=$(sed -n '/.*<em:id>\(.*\)<\/em:id>.*/{s//\1/p;q}' \
         extensions/globalmenu/package/install.rdf)
  install -dm755 "${pkgdir}/usr/lib/thunderbird/extensions/${emid}/"
  unzip -d "${pkgdir}/usr/lib/thunderbird/extensions/${emid}/" \
           dist/xpi-stage/globalmenu.xpi
}

# vim:set ts=2 sw=2 et:
