# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=ubuntu-wallpapers
pkgver=0.33.0
pkgrel=100
pkgdesc="The default wallpapers for Ubuntu"
arch=('any')
url="https://launchpad.net/ubuntu-wallpapers"
license=('GPL')
groups=('unity-extra')
makedepends=('libxslt' 'python-distutils-extra')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${pkgver}.tar.gz")
sha512sums=('c9ee5d0d71cbadda5102b30d91719ec8fd9a26ec35566f66ae7a7f1ac41a998de6c053941c3767bf18205637192dac19768f5e8246026729c2fe5acc312e2d52')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  xsltproc --novalid -o debian/gconf-defaults debian/wallpapers2gconf.xsl ubuntu-wallpapers.xml.in
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  python2 setup.py install --root="${pkgdir}/" --optimize=1
  install -Dm644 debian/gconf-defaults "${pkgdir}/usr/share/gconf/defaults/16_${pkgname}"
}

# vim:set ts=2 sw=2 et:
