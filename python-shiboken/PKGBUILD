# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgbase=python-shiboken
pkgname=('python-shiboken' 'python2-shiboken')
pkgver=1.2.1
pkgrel=100
pkgdesc="CPython bindings generator for C++ libraries"
arch=('i686' 'x86_64')
url="http://www.pyside.org/"
license=('LGPL')
groups=('unity-extra')
makedepends=('cmake' 'openssl' 'python' 'python2' 'qt4' 'libxslt')
# Official mirror temporarily broken
#source=("http://www.pyside.org/files/shiboken-${pkgver}.tar.bz2")
source=("https://distfiles.macports.org/py-shiboken/shiboken-${pkgver}.tar.bz2"
        '0001_Python_3.3.patch')
sha512sums=('485431b3d768ded859909342598fc42cccf6fb2901a3adefb0b2de0f01713eb420e14169957389d9c1c0c6c686b334b97826d875f9d8a330d308c280bcd10f7b'
            'ff42e293dec926fde7f3da4991345a00743090604395e517000d77d46d5c619f95a5f6d5d3a0cd878e2f68b725ffda9fa38d4aba1288324d3d3f16727a4153f6')

prepare() {
  cd "${srcdir}/shiboken-${pkgver}"

  patch -p1 -i "${srcdir}/0001_Python_3.3.patch"
}

build() {
  cd "${srcdir}/shiboken-${pkgver}"

  mkdir build-python2 build-python3

  pushd build-python2/
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DQT_QMAKE_EXECUTABLE=qmake-qt4

  make
  popd

  pushd build-python3/
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DUSE_PYTHON3:BOOL=TRUE

  make
  popd
}

package_python-shiboken() {
  depends=('openssl' 'python' 'qt4')

  cd "${srcdir}/shiboken-${pkgver}/build-python3"

  make DESTDIR="${pkgdir}/" install
}

package_python2-shiboken() {
  depends=('openssl' 'python2' 'qt4')

  cd "${srcdir}/shiboken-${pkgver}/build-python2"

  make DESTDIR="${pkgdir}/" install

  mv "${pkgdir}"/usr/bin/shiboken{,-py2}
  mv "${pkgdir}"/usr/include/shiboken{,-py2}
  mv "${pkgdir}"/usr/lib/cmake/Shiboken-${pkgver}/ShibokenConfig{,-py2}.cmake
  mv "${pkgdir}"/usr/lib/cmake/Shiboken-${pkgver}/ShibokenConfigVersion{,-py2}.cmake
  mv "${pkgdir}"/usr/lib/pkgconfig/shiboken{,-py2}.pc
  mv "${pkgdir}"/usr/share/man/man1/shiboken{,-py2}.1
}

# vim:set ts=2 sw=2 et:
