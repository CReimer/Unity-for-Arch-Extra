# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Contributor: twa022 <twa022 at gmail dot com>
# Contributor: thn81 <root@scrat>

pkgname=xfce4-indicator-plugin
pkgver=0.5.0
pkgrel=102
pkgdesc="Display indicators in the xfce4 panel"
arch=('i686' 'x86_64')
license=('LGPL2.1')
url="http://goodies.xfce.org/projects/panel-plugins/xfce4-indicator-plugin"
groups=('xfce4-goodies' 'unity')
depends=('xfce4-panel' 'libindicator')
makedepends=('intltool' 'xfce4-dev-tools')
groups=('unity')
options=('!libtool')
install=${pkgname}.install
source=("http://archive.xfce.org/src/panel-plugins/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2"
        '0001_autoconf.patch')
sha512sums=('807aa9f86d41753c01f96831bdd4c42823275314c15f8e0472330edd62a47a416619a98d7624687586d42d8ea290811c58d7f826edc51c884c2a60dbbd1b72c4'
            '94460ff07b48f0512e4180e80ff6ac1eee07dec40494a2dff99abe73e7de18f23f222dfdcc9520ee8f6f0c8007b3727edff7848eb6baca5bd562c107f7900558')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -p1 -i "${srcdir}/0001_autoconf.patch"
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  /usr/bin/xdt-autogen
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/${pkgname} \
    --localstatedir=/var \
    --disable-static \
    --disable-debug

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
