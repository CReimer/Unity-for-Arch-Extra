# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgbase=python-keyring
pkgname=('python-keyring' 'python2-keyring')
pkgver=1.3
pkgrel=100
pkgdesc="Python library for accessing keyring services"
arch=('any')
url="http://pypi.python.org/pypi/keyring"
license=('python')
groups=('unity-extra')
makedepends=('python' 'python2' 'python-distribute' 'python2-distribute' 'zip' 'unzip')
source=("http://pypi.python.org/packages/source/k/keyring/keyring-${pkgver}.zip")
noextract=("keyring-${pkgver}.zip")
sha512sums=('fdf295136d80f3a762598877f1791c5ab436e3fb68e5f8fd53c8711b8d0e72cd950fd3335f34bab0f77c86f2f27803c07ee8e520c240bbc7843ca78f840326d7')

package_python-keyring() {
  depends=('python-crypto')

  unzip "${srcdir}/keyring-${pkgver}.zip" -d "${srcdir}/build-python3/"
  cd "${srcdir}/build-python3/keyring-${pkgver}/"
  LANG="en_US.UTF-8" python setup.py install --root="${pkgdir}/" --optimize=1
}

package_python2-keyring() {
  depends=('python2-crypto')

  unzip "${srcdir}/keyring-${pkgver}.zip" -d "${srcdir}/build-python2/"
  cd "${srcdir}/build-python2/keyring-${pkgver}/"
  python2 setup.py install --root="${pkgdir}/" --optimize=1
  rm "${pkgdir}/usr/bin/keyring"
}

# vim:set ts=2 sw=2 et:
