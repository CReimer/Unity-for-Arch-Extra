# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Otiginal Maintainer: brainpower <brainpower@gulli.com>
# Original Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: Eric Bélanger <eric@archlinux.org>

pkgname=liferea-indicator
_ubuntu_rel=1ubuntu1
pkgver=1.8.15
pkgrel=100
pkgdesc="A desktop news aggregator for online news feeds and weblogs with Messages Indicator support"
arch=('i686' 'x86_64')
url="http://liferea.sourceforge.net/"
license=('GPL')
depends=('gconf' 'libwebkit' 'libnotify' 'libunique' 'json-glib' 'hicolor-icon-theme' 'indicator-messages' 'libunity')
makedepends=('intltool')
provides=("liferea=${pkgver}")
conflicts=('liferea')
options=('!libtool' '!emptydirs')
install=liferea.install
source=("http://downloads.sourceforge.net/sourceforge/liferea/liferea-${pkgver}.tar.bz2"
        "https://launchpad.net/ubuntu/+archive/primary/+files/liferea_${pkgver}-${_ubuntu_rel}.debian.tar.xz")
sha512sums=('af318a2b468b6d7c29528c44ab8cf8f34f853daceb7a34ccf52722af67b6d84fd04f9fe3eedc1ba27b1bc5561ea11bc2520ff107ae45eee2e69a426d57bb212a'
            '91df060445763eab7e48f54cfb0093814c1a384700fb77b4ec0428a41a33b6b482d7cddb3baaec20499a5734e61cd283e38f542b754548bf80c6160acc7032af')

prepare() {
  cd "${srcdir}/liferea-${pkgver}"

  # Apply Ubuntu's patches
    # Disable patches
      sed -i '/www-browser.patch/d' "${srcdir}/debian/patches/series"
  for i in $(grep -v '#' "${srcdir}/debian/patches/series"); do
    patch -p1 -i "${srcdir}/debian/patches/${i}"
  done
}

build() {
  cd "${srcdir}/liferea-${pkgver}"

  autoreconf -vfi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-schemas-install \
    --enable-nm \
    --enable-gnutls \
    --enable-lua \
    --enable-libnotify \
    --enable-messaging-menu

  make
}

package() {
  cd "${srcdir}/liferea-${pkgver}"
  GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 make DESTDIR="${pkgdir}" install

  install -dm755 "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" "${pkgdir}"/etc/gconf/schemas/*.schemas
  rm -v "${pkgdir}"/etc/gconf/schemas/*.schemas

  install -dm755 "${pkgdir}/usr/share/indicators/messages/applications/"
  install -m644 "${srcdir}/debian/liferea.indicate" \
    "${pkgdir}/usr/share/indicators/messages/applications/"

  install -dm755 "${pkgdir}/usr/share/liferea/pixmaps/"
  install -m644 "${srcdir}/debian/liferea.xpm" \
    "${pkgdir}/usr/share/liferea/pixmaps/"
}

# vim:set ts=2 sw=2 et:
