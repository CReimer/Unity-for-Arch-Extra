# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=empathy-ubuntu
_ubuntu_rel=0ubuntu2
pkgver=3.6.3
pkgrel=100.${_ubuntu_rel}
pkgdesc="A GNOME instant messaging client using the Telepathy framework."
arch=('i686' 'x86_64')
url="http://live.gnome.org/Empathy"
license=('GPL2')
provides=("empathy=${pkgver}")
conflicts=('empathy')
depends=('clutter-gst' 'clutter-gtk' 'folks' 'gcr' 'iso-codes' 'libcanberra' 'libpulse' 'webkitgtk3' 'telepathy-farstream' 'telepathy-glib' 'telepathy-logger' 'telepathy-mission-control' 'signon' 'libsignon-glib' 'credentials-preferences' 'libunity' 'ido')
makedepends=('intltool' 'itstool' 'docbook-xsl' 'nautilus-sendto')
optdepends=('telepathy-gabble: XMPP/Jabber support'
            'telepathy-idle: IRC support'
            'telepathy-salut: Link-local XMPP support'
            'telepathy-rakia: SIP support'
            'telepathy-haze: libpurple support'
            'nautilus-sendto: Send files from nautilus')
options=('!libtool')
groups=('gnome-extra')
install=empathy.install
source=("http://ftp.gnome.org/pub/GNOME/sources/${pkgname%-*}/${pkgver:0:3}/${pkgname%-*}-${pkgver}.tar.xz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/empathy_${pkgver}-${_ubuntu_rel}.debian.tar.bz2")
sha512sums=('888389b76ac84651ffcc31230a2d77069741acccd231bf1f86bce5c4e9e7be42e4794d65b0bb44e8dff519c52aceaea7e37bc71fa4f4b67b6a2df24818be2e26'
            'f802cfe64cfbdec4a6ddfb2c271eee7b6499f6f271556637cf90e8254575e7120c0880e6b4e8dfdd1c49ba35acf7bab24d8f880381fa4d5bd01d7bd9de19f6f8')

build() {
  cd "${pkgname%-*}-${pkgver}"

  # Apply Ubuntu's patches
  for i in $(grep -v '#' "${srcdir}/debian/patches/series"); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  autoreconf -vfi

  PYTHON=python2 ./configure \
      --prefix=/usr \
      --sysconfdir=/etc  \
      --libexecdir=/usr/lib/empathy \
      --disable-static \
      --disable-schemas-compile \
      --enable-gst-1.0=yes \
      --enable-spell=yes \
      --enable-webkit=yes \
      --enable-map=no \
      --enable-location=no \
      --enable-geocode=no \
      --enable-gudev=yes \
      --enable-call-logs=yes \
      --enable-call=yes \
      --enable-ubuntu-online-accounts=yes \
      --enable-goa=yes \
      --enable-libunity=yes \
      --enable-nautilus-sendto=yes \
      --enable-control-center-embedding=yes \
      --with-connectivity=nm

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make -j1
}

package() {
  cd "${pkgname%-*}-${pkgver}"
  make -j1 DESTDIR="${pkgdir}" install

  install -dm755 "${pkgdir}/usr/share/indicators/messages/applications/"
  install -m644 "${srcdir}/debian/indicators/empathy" \
    "${pkgdir}/usr/share/indicators/messages/applications/"
}

# vim:set ts=2 sw=2 et:
