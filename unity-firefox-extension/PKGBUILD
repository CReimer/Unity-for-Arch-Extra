# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# Upstream package name is unity-firefox-extension

pkgname=firefox-extension-unity
pkgver=2.3
pkgrel=100
pkgdesc="Firefox extension for Unity integration"
arch=('i686' 'x86_64')
url="https://launchpad.net/unity-firefox-extension"
license=('GPL')
groups=('unity-extra')
depends=('firefox-extension-websites-integration' 'libunity-webapps')
makedepends=('gtk2' 'libxslt' 'zip')
options=('!libtool')
source=("https://launchpad.net/unity-firefox-extension/trunk/${pkgver}/+download/unity-firefox-extension-${pkgver}.tar.gz")
sha512sums=('491c2419dbe4cd0497f5da13b9a117fb37ff8e15f6439a3d6a99742b45de3edb1723d6080e273a80360ba13b808774b0fc19552531e0603081b44fb4dae30446')

build() {
  cd "${srcdir}/unity-firefox-extension-${pkgver}"

  pushd libufe-xidgetter/
  autoreconf -vfi
  ./configure --prefix=/usr --disable-static
  make ${MAKEFLAGS}
  popd

  pushd unity-firefox-extension/
  bash ./build.sh
  popd
}

package() {
  cd "${srcdir}/unity-firefox-extension-${pkgver}"

  pushd libufe-xidgetter/
  make DESTDIR="${pkgdir}/" install
  popd

  pushd unity-firefox-extension/
  local emid=$(sed -n 's/.*<em:id>\(.*\)<\/em:id>.*/\1/p' install.rdf | head -1)
  install -dm755 "${pkgdir}/usr/lib/firefox/extensions/${emid}/"
  unzip unity.xpi -d "${pkgdir}/usr/lib/firefox/extensions/${emid}/"
  popd
}

# vim:set ts=2 sw=2 et:
