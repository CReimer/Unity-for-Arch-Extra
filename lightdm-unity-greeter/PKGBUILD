# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Taken from M0Rf30 morf3089 at gmail dot com

pkgname=lightdm-unity-greeter
_ubuntu_rel=0ubuntu2
pkgver=0.2.4.${_ubuntu_rel}
pkgrel=100
pkgdesc="The greeter (login screen) application for Unity. It is implemented as a LightDM greeter."
arch=('i686' 'x86_64')
url="https://launchpad.net/unity-greeter"
license=('GPL3' 'LGPL3')
groups=('unity-extra')
depends=('gnome-doc-utils' 'lightdm-ubuntu' 'gnome-common' 'libindicator' 'cantarell-fonts' 'light-themes' 'ubuntu-wallpapers' 'ttf-ubuntu-font-family')
makedepends=('gnome-settings-daemon' 'pkg-config' 'bzr' 'vala-git' 'autoconf')
options=(!libtool)
install=unity-greeter.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname#*-}_${pkgver%.*}-${_ubuntu_rel}.debian.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname#*-}_${pkgver%.*}.orig.tar.gz")
sha512sums=('6887d7ae0c4f41d2ec91ae53a7604069ddc8d0f2cff2758cc81af2d35ccd41bef388c06e81971950d65746e3089ee9a9ad4b3fd1d77ed74b0507db7f07701106'
            '21e20d109986ad418a0c446d8e5eca2c7c6cc80bb14d00764e53a50fc68f47ce8ae203dc5dd14cca0931a2851bf213e33d4ab0ccb98a74e83c101150438da1bf')

build() {
  cd "${srcdir}/${pkgname#*-}-${pkgver%.*}"

  # Apply Ubuntu patches
  for i in $(cat "${srcdir}/debian/patches/series" | grep -v '#'); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  # Link against libm and libx11
  CFLAGS="${CFLAGS} -lm -lX11"

  autoreconf -vfi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/lightdm

   make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/unity-greeter-${pkgver%.*}"

  make DESTDIR="${pkgdir}" install
}
