# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=ubuntu-mono
pkgver=0.0.49
pkgrel=100
pkgdesc="Icons for the panel, designed in a simplified monochrome style"
arch=('any')
url="https://launchpad.net/ubuntu-mono"
license=('CCPL')
groups=('unity-extra')
depends=('humanity-icon-theme' 'gnome-icon-theme' 'hicolor-icon-theme')
makedepends=('python2' 'imagemagick')
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${pkgver}.tar.gz")
sha512sums=('282f62b003d534f8c2ab0b82e4f9799a8085ddc213dacef9ab2633b5d10e9c8b1905137adc0423c989352a44397d08dafbf49375515181ffa60f1c924e4f44eb')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Python 2 fix
  sed -i 's|^\(#!.*python$\)|\12|g' scavenge.py

  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Install icons
  install -dm755 "${pkgdir}/usr/share/icons/"
  cp -av ubuntu-mono-dark "${pkgdir}/usr/share/icons/"
  cp -av ubuntu-mono-light "${pkgdir}/usr/share/icons/"
  cp -av LoginIcons "${pkgdir}/usr/share/icons/"

  for i in "${pkgdir}"/usr/share/icons/ubuntu-mono-*; do
    cp -avt ${i} debian/places/
  done
}

# vim:set ts=2 sw=2 et:
