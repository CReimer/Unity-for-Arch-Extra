# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

_ubuntu_ver=0.0.6

pkgname=unity-tweak-tool
pkgver=0.0.6
pkgrel=3
pkgdesc="Configuration tool for the Unity desktop"
arch=('any')
url="https://github.com/freyja-dev/unity-tweak-tool"
license=('GPL')
groups=('unity-extra')
depends=('glib2' 'gtk3' 'hicolor-icon-theme' 'notify-osd' 'python' 'python-xdg' 'python-dbus' 'unity')
# If these deps are missing, it will try to show a GTK popup, which obviously
# fails on a headless build server
makedepends=('python-distutils-extra' 'compiz' 'gnome-settings-daemon' 'gsettings-desktop-schemas' 'gtk2-ubuntu' 'hud' 'indicator-bluetooth' 'indicator-datetime' 'indicator-power' 'indicator-session' 'indicator-sound' 'libunity-webapps' 'notify-osd' 'overlay-scrollbar' 'unity' 'unity-lens-applications' 'unity-lens-files')
options=('!emptydirs')
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/unity-tweak-tool_${_ubuntu_ver}.tar.gz")
sha512sums=('198e27c8bfc79a6e7423b33c4abccb916f09ff609603b4e4d41060b1a9c3562b7c326d58b02cbbac5cdaa54c66ca6a03560c86ee663a26035a99a83f0436035d')

prepare() {
  cd "${srcdir}/${pkgname}"
  rm notes/wizardry.py
}

package() {
  cd "${srcdir}/${pkgname}"
  python setup.py install --root="${pkgdir}/" --optimize=1
}

# vim:set ts=2 sw=2 et:
