# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Some this was taken from Morfeo's lightdm PKDBUILD

pkgname=lightdm-ubuntu
_ubuntu_rel=0ubuntu1
pkgver=1.2.1.${_ubuntu_rel}
pkgrel=100
pkgdesc="A lightweight display manager"
arch=('i686' 'x86_64')
url="https://launchpad.net/lightdm"
license=('GPL3' 'LGPL3')
groups=('unity-extra')
depends=('gnome-themes-standard' 'gnome-backgrounds' 'gnome-icon-theme' 'libwebkit' 'dbus-glib' 'gtk3' 'libxklavier' 'intltool' 'accountsservice')
makedepends=('gnome-doc-utils' 'gobject-introspection' 'pkg-config' 'gtk-doc' 'gnome-common')
optdepends=('xorg-server-xephyr: run lightdm in test mode' 
            'lightdm-webkit-greeter-bzr'
            'lightdm-unity-greeter: default Ubuntu 12.04 greeter')
provides=("lightdm=${pkgver}")
conflicts=('lightdm')
options=('!libtool' '!emptydirs')
backup=('etc/apparmor.d/lightdm-guest-session'
        'etc/dbus-1/system.d/org.freedesktop.DisplayManager.conf'
        'etc/lightdm.conf'
        'etc/lightdm/keys.conf'
        'etc/lightdm/lightdm.conf'
        'etc/lightdm/lightdm-gtk-greeter.conf'
        'etc/lightdm/lightdm-gtk-greeter-ubuntu.conf'
        'etc/lightdm/users.conf')
install=lightdm.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname%-*}_${pkgver%.*}.orig.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname%-*}_${pkgver%.*}-${_ubuntu_rel}.diff.gz"
        'lightdm'
        'lightdm.service'
        'lightdm-gtk-greeter.conf'
        'lightdm.pam'
        'lightdm-autologin.pam'
        'guest-session-cross-distro.patch')
sha512sums=('23d0639631adccbe7205be000d8af866b03b7042b7eb11ff0b69bdf71b7351b8463442741ccf11b5c3deed19fdc083c8442ee550e21761f570b07cc3c169225d'
            'bd1eb9dac940029bd5253654e20f2ca804f8df20e217bbcad8c4e89defa26e5e5cad4b9d105ba7bec7e3f5955f6f011bb89d4cbe70e2d0dcf1b38c58eed5ee17'
            'c4813ae27d81ece8bf66e89c1adbb215443076cb6a2b1da28a5ea9528b25052715331a1d76711a2d06cbf9d41f46afa9a868b404112035db2295c177e7f99cc0'
            '0698d702540b5ea0f8935e8b26c3511f40c681dbf885241b63d4e73e94aff98bb37c8efe4c96f0d8caaefa7be774db49936921f46df3543191515f609154147f'
            'fa35ece114255abfc409f1c9da1eb7129055d8669aad11fe3d69084bf2216e93bf09864ac4e8874e88f166be9735fc55ed899056eb3bd94c5b33d3b2cbd55f4d'
            '7c97c028302d37235f550593f90cbe502abc9a1995d77aefedffa7e5635f043b4b61c7f8343db0084c561b3f46bbfc57db16a5623cba8d7aaec60e0e6af21853'
            '031f6a95f06f152c393d33b4d715863bf48df56c0de3bc80677f5101fb1cb2f7ae8f9c4a9c1944a998f063d84f9ccf04e453e7c78603067536260ff27d475e07'
            'b2a0d9c19e283cfc74f1a09876f93450f5af00de59869564f3b35bd07ce10e34f41a23396256b99ef1013a7dd164f19f0f2ee8b7d2fddd9fceafe1c327205092')

build() {
  cd "${srcdir}/${pkgname%-*}-${pkgver%.*}"

  # Apply Ubuntu patches
  patch -Np1 -i "${srcdir}/lightdm_${pkgver%.*}-${_ubuntu_rel}.diff"

  for i in $(cat 'debian/patches/series' | grep -v '#'); do
    patch -Np1 -i "debian/patches/${i}"
  done

  # Do not depend on Debian/Ubuntu specific adduser package
  patch -Np1 -i "${srcdir}/guest-session-cross-distro.patch"

  # Cannot find gmodule-2.0 with newer glib
  CFLAGS="${CFLAGS} $(pkg-config --cflags --libs gmodule-2.0)"

  autoreconf -vfi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --disable-static \
    --libexecdir=/usr/lib/lightdm \
    --localstatedir=/var \
    --with-user-session=ubuntu \
    --with-greeter-user=lightdm \
    --disable-tests # Tests fail with GCC 4.7 \
    #--with-greeter-session=lightdm-gtk-greeter

  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname%-*}-${pkgver%.*}"
  make DESTDIR="${pkgdir}/" install

  install -dm755 "${pkgdir}/etc/rc.d/"
  install -dm755 "${pkgdir}/lib/systemd/system/"

  # Move files (from debian/rules)
  mv "${pkgdir}/usr/lib/lightdm/lightdm/lightdm-set-defaults" "${pkgdir}/usr/lib/lightdm/"

  # Install PAM service
  install -dm755  "${pkgdir}/etc/pam.d/"
  install -m644 "${srcdir}/lightdm.pam" "${pkgdir}/etc/pam.d/lightdm"
  install -m644 "${srcdir}/lightdm-autologin.pam" "${pkgdir}/etc/pam.d/lightdm-autologin"

  # Install configuration files
  install -dm755 "${pkgdir}/usr/share/doc/lightdm/"
  install -dm755 "${pkgdir}/etc/lightdm/"
  install -m644 "${pkgdir}/etc/lightdm/lightdm.conf" "${pkgdir}/usr/share/doc/lightdm/"
  install -m644 "${pkgdir}/etc/lightdm/keys.conf" "${pkgdir}/usr/share/doc/lightdm/"
  install -m644 "${srcdir}/lightdm-gtk-greeter.conf" "${pkgdir}/etc/lightdm/"

  # Install binaries and scripts
  install -dm755 "${pkgdir}/usr/sbin/"
  install -dm755 "${pkgdir}/usr/lib/lightdm/"
  install -m755 "debian/lightdm-session" "${pkgdir}/usr/sbin/"
  install -m755 "debian/guest-account" "${pkgdir}/usr/sbin/"
  install -m755 "debian/lightdm-greeter-session" "${pkgdir}/usr/lib/lightdm/"
  chmod +x "${pkgdir}/usr/lib/lightdm/lightdm-greeter-session"

  # Install init script and SystemD service
  install -m755 ../lightdm $pkgdir/etc/rc.d
  install -m644 ../lightdm.service $pkgdir/lib/systemd/system/

  # Configuration settings that differ from Ubuntu
  sed -i \
    -e 's/^\(minimum-uid=\).*$/\11000/g' \
    -e 's@/usr\(/sbin/nologin\)$@\1@g' \
    "${pkgdir}/etc/lightdm/users.conf"

  # Configuration files specific to Ubuntu
  rm -rvf "${pkgdir}/etc/init/"
}

# vim:set ts=2 sw=2 et:
