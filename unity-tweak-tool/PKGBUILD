# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

_ubuntu_ver=0.0.6

pkgname=unity-tweak-tool
pkgver=0.0.6
pkgrel=5
pkgdesc="Configuration tool for the Unity desktop"
arch=('any')
url="https://github.com/freyja-dev/unity-tweak-tool"
license=('GPL')
groups=('unity-extra')
depends=('glib2' 'gtk3' 'hicolor-icon-theme' 'notify-osd' 'python' 'python-xdg' 'python-dbus' 'unity')
makedepends=('python-distutils-extra')
options=('!emptydirs')
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/unity-tweak-tool_${_ubuntu_ver}.tar.gz"
        '0001_Headless_build_fix.patch')
sha512sums=('198e27c8bfc79a6e7423b33c4abccb916f09ff609603b4e4d41060b1a9c3562b7c326d58b02cbbac5cdaa54c66ca6a03560c86ee663a26035a99a83f0436035d'
            '8e0106c865a4cf37944c561ea4be70f8f63872e5e70b00586cddbc4300e32b36779bfa3c90cfff52b6cce3b9eb4414793a2aaea55afc9a65cd36191f7eff4309')

prepare() {
  cd "${srcdir}/${pkgname}"
  patch -p1 -i "${srcdir}/0001_Headless_build_fix.patch"
}

package() {
  cd "${srcdir}/${pkgname}"
  export MAKEPKG=true
  python setup.py install --root="${pkgdir}/" --optimize=1
}

# vim:set ts=2 sw=2 et:
