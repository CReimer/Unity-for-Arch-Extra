# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Taken from M0Rf30 morf3089 at gmail dot com

pkgname=lightdm-unity-greeter
_ubuntu_rel=0ubuntu2
pkgver=13.04.1
pkgrel=100.${_ubuntu_rel}
pkgdesc="The greeter (login screen) application for Unity. It is implemented as a LightDM greeter."
arch=('i686' 'x86_64')
url="https://launchpad.net/unity-greeter"
license=('GPL3' 'LGPL3')
groups=('unity-extra')
depends=('gnome-doc-utils' 'lightdm-ubuntu' 'gnome-common' 'libindicator3' 'cantarell-fonts' 'light-themes' 'ubuntu-wallpapers' 'ttf-ubuntu-font-family')
makedepends=('gnome-settings-daemon' 'pkg-config' 'vala>=0.16.0' 'autoconf' 'archlinux-artwork' 'imagemagick')
options=('!libtool')
install=unity-greeter.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname#*-}_${pkgver}-${_ubuntu_rel}.debian.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname#*-}_${pkgver}.orig.tar.gz"
        '10-unity.defaults'
        '50-unity-greeter.rules'
        '0001_autoconf.patch')
sha512sums=('35a73b0240e453fd790d41be3db66a8380be976140cb5907aa8ffb68655147bdb94d05072ab8326e51fd146a439dc0e5b6ebc24c325b86be2cbd00ab2d0f58d0'
            'e3517b1b8bd3f6ffa8eda5b1470250d6192608d14e80430fd15bef13b6d0725aa1e5a43613dc23c905d9a2bdec6088eb1b6986b916d31d774544dbdce43aa6f7'
            'ee5d1f17dddd99ba55bbaca8aff5e8487c9b9f7e1eef570d2adb3d2519e19ef437b160414468ef85a8c1b14af1eee23c714e2086291701edfbab2f43064e2cd7'
            '5d0f1b8221dfe02670df3ba88011dc0ce744bfd4205a9900dd8096de222358a740a384a47f13ed4e5b94e24d4a8ff5639117464c61a689dd7bd9025a0900f529'
            'c31d52509484f26361de343f0bd53cba97ebe55ec7f028ec2afa166fec2f6d073e172788b5fcb2cd9bf7f4f2bab4a1f2268d30c3cefbbbd3e62948f64d0b1272')

build() {
  cd "${srcdir}/${pkgname#*-}-${pkgver}"

  # Apply Ubuntu patches
  for i in $(cat "${srcdir}/debian/patches/series" | grep -v '#'); do
    patch -p1 -i "${srcdir}/debian/patches/${i}"
  done

  patch -p1 -i "${srcdir}/0001_autoconf.patch"

  # Use Arch Linux logo (retain the same 245px height as the Ubuntu logo)
  rm data/logo.png
  convert -background none -resize 245 \
    /usr/share/archlinux/logos/archlinux-mono-white.svg \
    data/logo.png

  # Link against libm and libx11
  CFLAGS="${CFLAGS} -lm -lX11"

  autoreconf -vfi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/lightdm

   make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/unity-greeter-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # Install PolicyKit file for allowing the lightdm user to use NetworkManager
  # Note: PolicyKit no longer reads pkla files after version 0.107, so Ubuntu's
  # policy file won't work.
  install -dm700 "${pkgdir}/usr/share/polkit-1/rules.d/"
  install -m644 "${srcdir}/50-unity-greeter.rules" \
    "${pkgdir}/usr/share/polkit-1/rules.d/"
  #install -dm700 "${pkgdir}/var/lib/polkit-1/"
  #install -dm755 "${pkgdir}/var/lib/polkit-1/localauthority/10-vendor.d/"
  #install -m644 "${srcdir}/debian/unity-greeter.pkla" \
  #  "${pkgdir}/var/lib/polkit-1/localauthority/10-vendor.d/"

  # Install default GSettings/dconf settings for the guest account (requires
  # lightdm-ubuntu).
  install -dm755 "${pkgdir}/etc/guest-session/gsettings/"
  install -m644 "${srcdir}/10-unity.defaults" \
    "${pkgdir}/etc/guest-session/gsettings/"
}

# vim:set ts=2 sw=2 et:
