# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=accountsservice-ubuntu
_ubuntu_rel=0ubuntu6
pkgver=0.6.35
pkgrel=1
pkgdesc="D-Bus interface for user account query and manipulation"
arch=('i686' 'x86_64')
url="http://cgit.freedesktop.org/accountsservice/"
license=('GPL3')
depends=('glib2' 'polkit' 'systemd' 'lightdm-ubuntu')
makedepends=('intltool' 'gobject-introspection' 'vala')
provides=("accountsservice=${pkgver}")
conflicts=('accountsservice')
source=("http://cgit.freedesktop.org/accountsservice/snapshot/accountsservice-${pkgver}.tar.xz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/accountsservice_${pkgver}-${_ubuntu_rel}.debian.tar.gz"
        'avoid-deleting-the-root-user.patch')
sha512sums=('abd8fef4bf8a4588c51209718854d9acf1dc786a04175134539be55d2d2f57b761773116dc382880aef5a60c3689d8d789757f0db03113af5448ab68ed49b03b'
            '5c95b8a4e4b76cd65a5fb607b2dfdf7dde6b4a577c1b99315c6e2cac59158e0f9407dd729e85b0bdd747489f4f1b293167d81498bd7a8c140dfa544ea2d7d30a'
            'c687e152089288690f3b1cbbc8ffd9c2e10071f5f9647e2905a65c4630f75eb679630407243636f00badc50299ae43f7b350be9636f0a9c59cd080f9e1f3e9b7')

prepare() {
  cd "${srcdir}/accountsservice-${pkgver}"

  # Apply Arch Linux patches
  patch -p1 -i "${srcdir}/avoid-deleting-the-root-user.patch"

  # Apply Ubuntu patches
    # Disable patches
      # Arch Linux uses wheel group for system administration
        sed -i '/0002-create-and-manage-groups-like-on-a-debian-system.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
        sed -i '/0002-create-and-manage-groups-like-on-a-ubuntu-system.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
      # Arch Linux uses /etc/gdm as the GDM configuration directory
        sed -i '/0005-gdm_config_file_path.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
        sed -i '/0005-gdm_config_file_path_ubuntu.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
      # adduser is Debian-specific
        sed -i '/0006-adduser_instead_of_useradd.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
      # Language patches are still pretty specific to Ubuntu (Ubuntu is trying
      # to get them merged upstream)
        sed -i '/0001-formats-locale-property.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
        sed -i '/0009-language-tools.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
        sed -i '/0010-set-language.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
        sed -i '/0012-add-keyboard-layout-support.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
      # systemd can be used in Arch Linux
        sed -i '/2002-disable_systemd.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
      # Does not apply cleanly
        sed -i '/0013-add-has-message-support.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
        sed -i '/2001-filtering_out_users.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
      # We do not use Debian/Ubuntu paths
        sed -i '/0015-pam-pin-ubuntu.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
      # Causes build failure (and I'm not sure why anyone would use a less
      # secure pin authentication module on anything other than a phone)
        sed -i '/0014-pam-pin.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
        sed -i '/0016-add-input-sources-support.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
    # Fix patches
      #sed -i 's,/etc/gdm3/daemon\.conf,/etc/gdm/custom.conf,g' \
      #  "${srcdir}"/debian/patches/2001-filtering_out_users.patch

  for i in $(grep -v '#' "${srcdir}/debian/patches/ubuntu.series"); do
    msg "Applying ${i} ..."
    patch -p1 -i "${srcdir}/debian/patches/${i}"
  done
}

build() {
  cd "${srcdir}/accountsservice-${pkgver}"

  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/accountsservice \
    --with-systemdsystemunitdir=/usr/lib/systemd/system \
    --localstatedir=/var \
    --disable-static \
    --enable-systemd

  make
}

package() {
  cd "${srcdir}/accountsservice-${pkgver}"

  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
