# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=webapps-applications
_actual_ver=2.4.10
_ubuntu_rel=0ubuntu3
pkgver=${_actual_ver}.${_ubuntu_rel}
pkgrel=100
pkgdesc="Unity WebApps integration scripts"
arch=('any')
url="https://launchpad.net/webapps-applications"
license=('GPL')
groups=('unity-extra')
depends=('libunity-webapps' 'xdg-utils')
makedepends=('python-polib' 'intltool')
install=webapps-applications.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/webapps-applications_${_actual_ver}.orig.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/webapps-applications_${_actual_ver}-${_ubuntu_rel}.debian.tar.gz")
sha512sums=('10585064b7d78623f955813048b4160ae9530e4b5e7e5b2b4d41878de721cdbca1bddda4353ab843f5e14e5f9f20dfda34bb62e6355496670f3c7dd6ec7113ef'
            '547c0381b32739e03c8953fcf9fcc70b119f9ecabc720186e546312cbb35e0cc3dea64415258c5cccef175ce4f49819ca361d1a364d7182d4993dfd32177c48e')

build() {
  cd "${srcdir}/webapps-${_actual_ver}"

  # Apply Ubuntu patches
  for i in $(grep -v '#' "${srcdir}/debian/patches/series"); do
    patch -p1 -i "${srcdir}/debian/patches/${i}"
  done

  autoreconf -vfi
  intltoolize -f

  ./configure --prefix=/usr --enable-default-applications
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/webapps-${_actual_ver}"
  make DESTDIR="${pkgdir}/" install

  install -dm755 "${pkgdir}/usr/bin/"
  install -m755 scripts/install-default-webapps-in-launcher.py \
    "${pkgdir}/usr/bin/webapps-add-defaults-to-launcher"

  # What is heck is zn_CN? zh stands for the first two letter of the pinyin of
  # 中文 (zhongwen)
  mv "${pkgdir}"/usr/share/locale/{zn,zh}_CN/
}

# vim:set ts=2 sw=2 et:
