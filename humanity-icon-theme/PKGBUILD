# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=humanity-icon-theme
pkgver=0.6.3
pkgrel=100
pkgdesc="Humanity Icon Theme"
arch=('any')
url="https://launchpad.net/humanity"
license=('GPL')
groups=('unity-extra')
depends=('gnome-icon-theme' 'hicolor-icon-theme')
makedepends=('icon-naming-utils')
provides=("humanity-icons=${pkgver}")
conflicts=('humanity-icons')
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${pkgver}.tar.gz")
sha512sums=('f9c04921355b186c4690a065cc701980836c3d0d6319fddaab4d4ec76bc7fb62de10148ae11b21e20de5ac47fbff0bf162a947170b64fa7f28a1f217a963155e')

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  install -dm755 "${pkgdir}"/usr/share/icons/

  # Install Humanity icon theme
  cp -a Humanity/ "${pkgdir}"/usr/share/icons/

  # Install Humanity-Dark icon theme
  cp -a Humanity-Dark/ "${pkgdir}"/usr/share/icons/

  # From debian/rules
  for i in $(find "${pkgdir}/usr/share/icons/" -mindepth 2 -maxdepth 2 -type d); do
    (
    cd "${i}"
    for j in *; do
      /usr/lib/icon-naming-utils/icon-name-mapping -c "${j}"
    done
    )
  done
}

# vim:set ts=2 sw=2 et:
