# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

_bzr_rev=147

pkgname=thunderbird-messagingmenu
pkgver=1.0.1
pkgrel=101
pkgdesc="Messaging Menu Thunderbird Extension"
arch=('i686' 'x86_64')
url="https://launchpad.net/messagingmenu-extension"
license=('MPL')
groups=('unity-extra')
depends=('thunderbird' 'indicator-messages')
makedepends=('bzr' 'unzip' 'zip')
provides=("messagingmenu-extension=${pkgver}")
conflicts=('messagingmenu-extension')
source=("messagingmenu-extension::bzr+http://bazaar.launchpad.net/~extension-hackers/messagingmenu-extension/1.0/#${_bzr_rev}")
sha512sums=('SKIP')

build() {
  cd "${srcdir}/messagingmenu-extension"

  sh build.sh
}

package() {
  cd "${srcdir}/messagingmenu-extension"

  EMID=$(sed -n 's/^.*<em:id>\(.*\)<\/em:id>.*$/\1/p' install.rdf | head -n 1)

  install -dm755 "${pkgdir}/usr/lib/thunderbird/extensions/${EMID}/"
  unzip -d "${pkgdir}/usr/lib/thunderbird/extensions/${EMID}/" \
    messagingmenu.xpi
}

# vim:set ts=2 sw=2 et:
